// enterpriseLang
// Authors: Wenjun Xiong (wenjx@kth.se), Emeline Legrand, Oscar Åberg
// Probability distributions: Quentin Biharé, Love Wessman, Lina Evensjö

#id: "org.mal_lang.attacklang"
#version: "1.0.0"

category Person {
    asset User {
        & spearphishingLinkClicked [Bernoulli(0.712)*Exponential(1)]
            developer info: "Sources for distribution result: https://arxiv.org/pdf/1606.00887; https://dl.acm.org/doi/fullHtml/10.1145/1290958.1290968"
            ->  attemptUserExecution,
                computer.os.service.thirdpartySoftware.applicationAccessToken,
                computer.os.service.exploitationForClientExecution

        & spearphishingAttachmentDownload [Bernoulli(0.93)*Exponential(1)]
            developer info: "Sources for distribution result: Based on attack step spearphishingLinkClicked and https://doi.org/10.1016/j.cose.2006.10.009"
            ->  attemptUserExecution,
                computer.os.windows.dynamicDataExchange

        | attemptUserExecution @hidden
            ->  userAccount.userExecution

        | mediaInserted [Bernoulli(0.3)*Exponential(48)]
            user info: "People work in the targeted entreprise have physical access to the network and can thus easily use an infected USB device for the adversary to get initial compromise."
            developer info: "Sources for distribution result: https://www.supplychaindive.com/news/internal-actors-cyberattacks-manufacturing/572832/#:~:text=External%20actors%20account%20for%20the,2019%20Data%20Breach%20Investigations%20Report"
            ->  computer.os.windows.replicationThroughRemovableMedia,
                computer.peripheralDevice.removableMedia.dataExfiltration

        # userTraining [Bernoulli(0.22)]
            user info: "https://attack.mitre.org/mitigations/M1017/"
            developer info: "Sources for distribution result: https://www.tandfonline.com/doi/abs/10.1080/10919392.2019.1552745; https://doi.org/10.1145/1572532.1572536; https://ieeexplore.ieee.org/abstract/document/6585241" 
            ->  spearphishingAttachmentDownload, 
                spearphishingLinkClicked, 
                userAccount.userExecution
    }
}

category Software {
    asset UserAccount {
        | userRights
            ->  os.accountAccessRemoval,
                os.accountDiscovery,
                os.applicationWindowDiscovery,
                os._automatedCollection,
                os.attemptBruteForce,
                os.compileAfterDelivery,
                os._credentialsInFiles,
                os._credentialsInRegistry,
                os._dataEncoding,
                os.dataEncryptedForImpact,
                os.dataDestruction, 
                os.diskContentWipe,
                os.domainDiscovery,
                os.domainGenerationAlgorithms,
                os.executionGuardrails,
                os.fileDeletion,
                os.fileAndDirectoryDiscovery,
                os.fileAndDirectoryPermissionsModification,
                os.graphicalUserInterface,
                os.hiddenFilesAndDirectories,
                os.inhibitSystemRecovery,
                os.internalSpearphishing,
                os.installRootCertificate,
                os.passwordPolicyDiscovery,
                os.permissionGroupsDiscovery,
                os.privateKeys,
                os.processInjection,
                os.processDiscovery,
                os.runtimeDataManipulation,
                os.redundantAccess,
                os.remoteSystemDiscovery,
                os.resourceHijacking,
                os.rootkit,
                os.scripting,
                os.softwareDiscovery,
                os.systemInformationDiscovery,
                os.systemNetworkConfigurationDiscovery,
                os.systemNetworkConnectionsDiscovery,
                os.systemOwnerOrUserDiscovery,
                os.systemShutdownOrReboot,
                os.storedDataManipulation,
                os.remoteFileCopy, 
                os.userCommandLineInterface,
                os.windows.accountManipulation,
                os.windows._bITSJobs,
                os.windows.bypassUserAccountControl,
                os.windows.changeDefaultFileAssociation,
                os.windows.cmstp,
                os.windows.compiledHTMLFile,
                os.windows._componentObjectModelAndDistributedCOM,
                os.windows.componentObjectModelHijacking,
                os.windows.controlPanelItems,
                os.windows.deobfuscateOrDecodeFilesOrInformation,
                os.windows._dLLSearchOrderHijacking,
                os.windows.domainTrustDiscovery,
                os.windows.dynamicDataExchange,  
                os.windows.emailStorageCollection,
                os.windows.mshta,
                os.windows.executionThroughAPI,
                os.windows.executionThroughModuleLoad,
                os.windows._fileSystemPermissionsWeakness,
                os.windows.forcedAuthentication,
                os.windows.groupPolicyModification,
                os.windows.indirectCommandExecution,
                os.windows.inputPrompt,
                os.windows.installUtil,
                os.windows.kerberoasting,
                os.windows.lLMNR_NBT_NS_PoisoningAndRelay,
                os.windows.logonScripts,
                os.windows.modifyRegistry,
                os.windows.networkShareConnectionRemoval,
                os.windows.networkShareDiscovery,
                os.windows.officeApplicationStartup,
                os.windows.parentPIDSpoofing,
                os.windows.pathInterception,
                os.windows.peripheralDeviceDiscovery,
                os.windows.powerShell,
                os.windows.powerShellProfile,
                os.windows.processDoppelganging,
                os.windows.processHollowing,
                os.windows.registryRunKeysOrStartupFolder,
                os.windows.regsvcsOrRegasm,
                os.windows.regsvr32,
                os.windows.rundll32,
                os.windows.scheduledTask,
                os.windows.screensaver,
                os.windows.shortcutModification,
                os.windows.signedBinaryProxyExecution,
                os.windows.signedScriptProxyExecution,
                os.windows.sIPAndTrustProviderHijacking,
                os.windows.systemServiceDiscovery,
                os.windows._taintSharedContent,
                os.windows.templateInjection,
                os.windows.timestomp,
                os.windows.trustedDeveloperUtilities,
                os.windows.attemptAccessTokenManipulation,
                os.windows.securitySoftwareDiscovery,
                os.windows.serviceStop,
                os.windows.videoCapture,
                os.windows.windowsManagementInstrumentation,
                os.windows.windowsRemoteManagement,
                os.windows.xslScriptProcessing,
                os.linux._bash_profileAndBashrc,
                os.linux._clearCommandHistory,
                os.linux.hISTCONTROL,
                os.linux.localJobScheduling,
                os.linux.portKnocking,
                os.linux.setuidAndSetgid,
                os.linux.spaceAfterFileName,
                os.linux.source,
                os.linux.sshHijacking,
                os.linux._sudo,
                os.linux._sudoCaching,
                os.linux._systemdService,
                os.linux.timestomp,
                os.linux.trap,
                os.macOS.appleScript,
                os.macOS._bash_profileAndBashrc,
                os.macOS._clearCommandHistory,
                os.macOS._dylibHijacking,
                os.macOS.elevatedExecutionWithPrompt,
                os.macOS.gatekeeperBypass,
                os.macOS.hiddenWindow,
                os.macOS.hISTCONTROL,
                os.macOS.inputPrompt,
                os.macOS.launchAgent,
                os.macOS.launchctl,
                os.macOS.lC_LOAD_DYLIB_Addition,
                os.macOS.lC_MAINHijacking,
                os.macOS.localJobScheduling,
                os.macOS.loginItem,
                os.macOS.logonScripts,
                os.macOS.plistModification,
                os.macOS.portKnocking,
                os.macOS.spaceAfterFileName,
                os.macOS.networkShareDiscovery,
                os.macOS.videoCapture,
                os.macOS.reopenedApplications,
                os.macOS.setuidAndSetgid,
                os.macOS.source,
                os.macOS.sshHijacking,
                os.macOS._sudo,
                os.macOS._sudoCaching,
                os.macOS.trap,
                os.service.cloudInstanceMetadataAPI,
                os.service._exploitationForCredentialAccess,
                os.service._exploitationForDefenseEvasion,
                os.service.attemptExploitationForPrivilegeEscalation,
                os.service._exploitationOfRemoteServices,
                os.service.externalRemoteServices,
                os.service.implantContainerImage,
                os.service.remoteAccessTools,
                os.service.thirdpartySoftware.stealApplicationAccessToken,
                os.service.thirdpartySoftware._useThirdpartySoftware,
                os.service.browser.credentialsFromWebBrowsers,
                os.service.browser.driveByCompromise,
                os.service.browser.installExtensions,
                os.service.browser.stealWebSessionCookie,
                os.computer.router.internalNetwork.transmittedDataManipulation,
                os.computer.router.externalNetwork.dataFromCloudStorageObject,
                os.computer.router.externalNetwork.transmittedDataManipulation,
                os.computer.router.externalNetwork.webService

        | userCredentials
            ->  userRights,
                os._validAccounts,  
                os.windows.outlookEmailCollection,
                os.computer.router.internalNetwork.bypassAccessControl,
                os.computer.router.internalNetwork.exchangeServerCollection,
                os.service.userRemoteServicesLogin

        & userExecution
            developer info: "Binary probability relation."
            ->  os.computer.infectedComputer,
                os.computer.infectedWindowsComputer,
                os.executeCode

        | userInformation

        # userTraining [Bernoulli(0.22)]
            user info: "https://attack.mitre.org/mitigations/M1017/"
            developer info: "Sources for distribution result: https://www.tandfonline.com/doi/abs/10.1080/10919392.2019.1552745; https://doi.org/10.1145/1572532.1572536; https://ieeexplore.ieee.org/abstract/document/6585241"      
            ->  os.service.browser.stealWebSessionCookie,
                os.credentialDumping,
                os.credentialsInFiles,
                os.twoFactorAuthenticationInterception, // remove smart cards when not in use
                os.windows.inputPrompt,
                os.windows.manInTheBrowser,
                os.windows.templateInjection,
                os.macOS.inputPrompt, 
                os.macOS.loginItem,
                os.macOS.reopenedApplications, 
                os.service.browser.browserExtensions,
                os.service.spearphishingViaService,
                os.service.thirdpartySoftware.useThirdpartySoftware,
                os.service.thirdpartySoftware.stealApplicationAccessToken   

        # userAccountManagement [Bernoulli(0.30)] 
            developer info: "Sources for distribution results : https://lp.alpha-gen.co.uk/2018-global-state-of-pam/"
            user info: "https://attack.mitre.org/mitigations/M1018/"     
            ->  os.disablingSecurityTools,
                os.validAccounts,
                os.computer.router.externalNetwork.dataFromCloudStorageObject,
                os.windows.serviceStop,
                os.windows._bITSJobs,
                os.windows.fileSystemPermissionsWeakness,
                os.windows.indicatorBlocking,
                os.windows.scheduledTask,
                os.windows.manInTheBrowser,
                os.windows.modifyExistingService,
                os.windows.newService,
                os.windows.passTheHash,
                os.windows.passTheTicket,
                os.windows.pathInterception,
                os.windows.attemptAccessTokenManipulation,
                os.windows.remoteDesktopProtocol,
                os.windows.remoteScheduleTask,
                os.windows.sharedWebroot,
                os.windows.shortcutModification,
                os.windows.windowsManagementInstrumentation,
                os.windows.windowsManagementInstrumentationEventSubscription,
                os.windows.winlogonHelperDLL,
                os.linux.localJobScheduling,
                os.linux.systemdService,
                os.macOS._dylibHijacking,
                os.macOS.launchAgent,
                os.macOS.launchctl,
                os.macOS.launchDaemon,
                os.macOS.localJobScheduling,
                os.macOS.loginItem,
                os.macOS.rc_common,
                os.macOS._startupItems,
                os.service.userRemoteServicesLogin,
                os.service.adminRemoteServicesLogin,
                os.service.thirdpartySoftware.stealApplicationAccessToken,
                os.service.thirdpartySoftware._useThirdpartySoftware 
    } 

    asset RemoteDesktopUser extends UserAccount {
        | userRights
            +>  os.windows.remoteDesktopProtocol

        # multiFactorAuthentication
            ->  os.windows.remoteDesktopProtocol
    }     

    asset AdminAccount {
        | adminRights
            user info: "Administrator is an actual account."
            ->  userAccount.userRights,
                createUserAccount, 
                os.accountAccessRemoval,   
                os._automatedCollection,
                os.adminCommandLineInterface,
                os.dataDestruction,
                os.dataEncryptedForImpact,
                os.attemptCredentialDumping,
                os._credentialsInFiles,
                os._credentialsInRegistry,
                os.processDiscovery,
                os.detailedRemoteSystemDiscovery,
                os.diskContentWipe,
                os.diskStructureWipe,
                os.fileAndDirectoryDiscovery,
                os.fileAndDirectoryPermissionsModification,
                os.firmwareCorruption, 
                os.graphicalUserInterface,  
                os.inhibitSystemRecovery, 
                os.inputCapture,
                os.installRootCertificate,            
                os.networkServiceScan,
                os.networkSniffing,
                os.redundantAccess,
                os.resourceHijacking,
                os.runtimeDataManipulation,
                os.softwareDiscovery,
                os.storedDataManipulation,
                os.systemNetworkConnectionsDiscovery,
                os.systemOwnerOrUserDiscovery,
                os.systemShutdownOrReboot,
                os.twoFactorAuthenticationInterception,
                os.service.transmittedDataManipulation,
                os.service.applicationDeploymentSoftware,
                os.service.thirdpartySoftware._useThirdpartySoftware,
                os.computer.router.internalNetwork.transmittedDataManipulation,
                os.computer.router.internalNetwork.bypassAccessControl,
                os.computer.router.externalNetwork.transmittedDataManipulation
             
        | adminCredentials
            ->  adminRights,
                os._validAccounts,
                os.service.adminRemoteServicesLogin,
                os.computer.router.internalNetwork.bypassAccessControl
            
        & createUserAccount 
            ->  os.persistence          
        
        # multiFactorAuthentication
            user info: "https://attack.mitre.org/mitigations/M1032/"
            ->  createUserAccount

        # privilegedAccountManagement [Bernoulli(0.30)]
            developer info: "Sources for distribution results : https://lp.alpha-gen.co.uk/2018-global-state-of-pam/"
            user info: "https://attack.mitre.org/mitigations/M1026/"
            ->  createUserAccount,
                os.credentialDumping,
                os.firmwareCorruption,
                os.windows.accountManipulation,
                os.windows.modifyRegistry,
                os.windows.passTheTicket,
                os.windows.powerShell,
                os.windows.remoteDesktopProtocol,
                os.windows.serviceExecution,
                os.windows.serverSoftwareComponent,
                os.windows.signedBinaryProxyExecution, 
                os.windows.sharedWebroot,  
                os.windows.systemFirmware,
                os.windows.attemptAccessTokenManipulation,
                os.windows.windowsAdminShares,
                os.windows.windowsManagementInstrumentation,
                os.windows.windowsManagementInstrumentationEventSubscription,
                os.windows.windowsRemoteManagement,
                os.linux.kernelModulesAndExtensions,
                os.linux.serverSoftwareComponent,
                os.linux.sudo,
                os.linux.sudoCaching,
                os.linux.systemdService,
                os.macOS.kernelModulesAndExtensions,
                os.macOS.lC_MAINHijacking,
                os.macOS.sudo,
                os.macOS.sudoCaching,
                os.service.applicationDeploymentSoftware,
                os.service._exploitationOfRemoteServices,
                os.service.thirdpartySoftware._useThirdpartySoftware, // grant access to application deployment systems only to a limited number of authorized administrators
                os.service.exploitHighVulnerabilityPublicFacingApplication,
                os.service.exploitMediumVulnerabilityPublicFacingApplication,
                os.service.exploitLowVulnerabilityPublicFacingApplication
    }
   
    asset WindowsAdmin extends AdminAccount {
        | adminRights
            user info: "WindowsAdmin account is equivalent to Unix Root account."
            +>  os.windows.attemptAccessibilityFeatures,
                os.windows.attemptAccessTokenManipulation,
                os.windows.securitySoftwareDiscovery,
                os.windows.serviceStop,
                os.windows._appCertDLLs,
                os.windows.appInitDLLs,
                os.windows.attemptApplicationShimming,
                os.windows.authenticationPackage,
                os.windows._bITSJobs,
                os.windows.bootkit,                                          
                os.windows.bypassUserAccountControl,
                os.windows.changeDefaultFileAssociation,
                os.windows.componentObjectModelAndDistributedCOM,
                os.windows.controlPanelItems,
                os.windows.dCShadow,
                os.windows._dLLSearchOrderHijacking,
                os.windows.executionThroughAPI,
                os.windows.extraWindowMemoryInjection,
                os.windows.fileSystemLogicalOffsets,
                os.windows._fileSystemPermissionsWeakness,
                os.windows.groupPolicyModification,
                os.windows.hooking,
                os.windows.hypervisor,
                os.windows.imageFileExecutionOptionsInjection,
                os.windows.lSASS_Driver,
                os.windows.manInTheBrowser,
                os.windows.modifyExistingService,
                os.windows.modifyRegistry,
                os.windows.netshHelperDLL,
                os.windows.networkShareConnectionRemoval,
                os.windows.newService,
                os.windows.officeApplicationStartup,
                os.windows.parentPIDSpoofing,
                os.windows.passwordFilterDLL,
                os.windows.pathInterception,
                os.windows.portMonitors,
                os.windows.powerShell,
                os.windows.powerShellProfile,
                os.windows.processDoppelganging,
                os.windows.queryRegistery,
                os.windows.remoteScheduleTask,
                os.windows.registryRunKeysOrStartupFolder,
                os.windows.regsvcsOrRegasm,
                os.windows.regsvr32,
                os.windows.securitySupportProvider,
                os.windows.serverSoftwareComponent,
                os.windows.serviceExecution,
                os.windows.serviceRegistryPermissionsWeakness,
                os.windows.shortcutModification,
                os.windows.sIDHistoryInjection,
                os.windows.scheduledTask,
                os.windows.systemFirmware,
                os.windows.systemServiceDiscovery,
                os.windows.timeProviders,
                os.windows.timestomp,
                os.windows.windowsAdminShares,
                os.windows.windowsManagementInstrumentation,
                os.windows.windowsManagementInstrumentationEventSubscription,
                os.windows.windowsRemoteManagement,
                os.windows.winlogonHelperDLL

        | adminCredentials

        # multiFactorAuthentication
            +>  os.windows.accountManipulation       
    }
    
    asset Root extends AdminAccount {
        | adminRights
            user info: "Unix Root account is equivalent to WindowsAdmin account."
            +>  os.linux.kernelModulesAndExtensions,
                os.linux.serverSoftwareComponent,
                os.linux._systemdService,
                os.linux.sshHijacking,
                os.linux.timestomp,
                os.linux.trap,
                os.linux._bash_profileAndBashrc,
                os.linux.bootkit,
                os.linux.localJobScheduling,
                os.macOS.securitySoftwareDiscovery,
                os.macOS._bash_profileAndBashrc,
                os.macOS.emond,
                os.macOS.keychainCollection,
                os.macOS.localJobScheduling,
                os.macOS.gatekeeperBypass,
                os.macOS.hiddenUsers,
                os.macOS.kernelModulesAndExtensions,
                os.macOS.launchAgent,
                os.macOS.launchctl,
                os.macOS.launchDaemon,
                os.macOS.plistModification,
                os.macOS.rc_common,
                os.macOS.securitydMemory,
                os.macOS.sshHijacking,
                os.macOS._startupItems,
                os.macOS.trap
    }

    asset Service {
        & applicationDeploymentSoftware
            user info: "Adversaries may deploy malicious software to systems within a network using application deployment systems employed by enterprise administrators."
            ->  os.executeCode

        | applicationHighVulnerability [Exponential(24)]
            developer info: "Sources for distribution results : https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf; https://www.ptsecurity.com/ww-en/analytics/web-vulnerabilities-2020/#:~:text=The%20average%20number%20of%20vulnerabilities,five%20vulnerabilities%20has%20high%20severity"              
            ->  exploitHighVulnerabilityPublicFacingApplication

        | applicationMediumVulnerability [Exponential(2)]
            developer info: "Sources for distribution results : https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf; https://www.ptsecurity.com/ww-en/analytics/web-vulnerabilities-2020/#:~:text=The%20average%20number%20of%20vulnerabilities,five%20vulnerabilities%20has%20high%20severity"              
            ->  exploitMediumVulnerabilityPublicFacingApplication

        | applicationLowVulnerability [Exponential(1)]
            developer info: "Sources for distribution results : https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf; https://www.ptsecurity.com/ww-en/analytics/web-vulnerabilities-2020/#:~:text=The%20average%20number%20of%20vulnerabilities,five%20vulnerabilities%20has%20high%20severity"              
            ->  exploitLowVulnerabilityPublicFacingApplication

        & exploitHighVulnerabilityPublicFacingApplication [Bernoulli(0.5)*Exponential(2)]
            user info: "The use of software, data, or commands to take advantage of a weakness in an Internet-facing computer system or program in order to cause unintended or unanticipated behavior."
            developer info: "Sources for distribution results : https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf; https://www.ptsecurity.com/ww-en/analytics/web-vulnerabilities-2020/#:~:text=The%20average%20number%20of%20vulnerabilities,five%20vulnerabilities%20has%20high%20severity"              
            ->  exploitationForDefenseEvasion, // leads to exploitation for defense evasion attack
                os.systemAccess
                
        & exploitMediumVulnerabilityPublicFacingApplication [Bernoulli(0.87)*Exponential(0.5)]
            user info: "The use of software, data, or commands to take advantage of a weakness in an Internet-facing computer system or program in order to cause unintended or unanticipated behavior."
            developer info: "Sources for distribution results : https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf; https://www.ptsecurity.com/ww-en/analytics/web-vulnerabilities-2020/#:~:text=The%20average%20number%20of%20vulnerabilities,five%20vulnerabilities%20has%20high%20severity"              
            ->  exploitationForDefenseEvasion, // leads to exploitation for defense evasion attack
                os.systemAccess
                
        & exploitLowVulnerabilityPublicFacingApplication [Bernoulli(0.89)*Exponential(0.143)]
            user info: "The use of software, data, or commands to take advantage of a weakness in an Internet-facing computer system or program in order to cause unintended or unanticipated behavior."
            developer info: "Sources for distribution results : https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf; https://www.ptsecurity.com/ww-en/analytics/web-vulnerabilities-2020/#:~:text=The%20average%20number%20of%20vulnerabilities,five%20vulnerabilities%20has%20high%20severity"              
            ->  exploitationForDefenseEvasion, // leads to exploitation for defense evasion attack
                os.systemAccess
                
        | blockUserAccess  

        & cloudInstanceMetadataAPI
            user info: "Adversaries may attempt to access the Cloud Instance Metadata API to collect credentials and other sensitive data."
            ->  os.sensitiveDataCollected

        & implantContainerImage [Bernoulli(0.60)*Exponential(180)]
            user info: "Amazon Web Service (AWS) Amazon Machine Images (AMI), Google Cloud Platform (GCP) Images, and Azure Images as well as popular container runtimes such as Docker can be implanted or backdoored to include malicious code."
            developer info: "Sources for distribution result: https://www.eweek.com/security/five-security-defenses-every-containerized-application-needs, https://github.com/RhinoSecurityLabs/ccat, https://www.youtube.com/watch?v=12zEXfCxIrk&feature=youtu.be"
            ->  os.persistence

        | serviceInformation 

        & userRemoteServicesLogin
            user info: "An adversary may use Valid Accounts to log into a service specifically designed to accept remote connections."
            ->  os.computer.router.internalNetwork.c2Connected 

        & adminRemoteServicesLogin
            user info: "An adversary may use Valid Accounts to log into a service specifically designed to accept remote connections."
            ->  os.computer.router.internalNetwork.c2Connected 

        & transmittedDataManipulation {I}
            user info: "By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making."

        | informationRepositories
            ->  os.dataCollected

        | remoteAccessTools
            ->  os.computer.router.internalNetwork.c2Connexion

        | serviceExhaustionFlood

        | attemptSpearphishingViaService @hidden
            ->  spearphishingViaService

        & spearphishingViaService
            user info: "Adversaries send messages through various social media services, personal webmail, and other non-enterprise controlled services."
            ->  spearphishingViaPersonalEmail,
                spearphishingViaSocialMedia,
                os.computer.user.attemptUserExecution
        
        | spearphishingViaPersonalEmail [Bernoulli(0.712)*Exponential(1)]
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/5665797; https://www.sans.org/reading-room/whitepapers/analyst/endpoint-protection-response-survey-38460; https://www.businesswire.com/news/home/20100804005348/en/Cyveillance-Testing-Finds-AV-Vendors-Detect-Average; https://academic.oup.com/jcmc/article/20/1/83/4067561; https://www.blackhat.com/docs/us-16/materials/us-16-Seymour-Tully-Weaponizing-Data-Science-For-Social-Engineering-Automated-E2E-Spear-Phishing-On-Twitter-wp.pdf"
            ->  browser.attemptSpearphishingAttachment,
                browser.attemptSpearphishingLink
        
        | spearphishingViaSocialMedia [Bernoulli(0.4475)*Exponential(0.286)]
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/5665797; https://www.sans.org/reading-room/whitepapers/analyst/endpoint-protection-response-survey-38460; https://www.businesswire.com/news/home/20100804005348/en/Cyveillance-Testing-Finds-AV-Vendors-Detect-Average; https://academic.oup.com/jcmc/article/20/1/83/4067561; https://www.blackhat.com/docs/us-16/materials/us-16-Seymour-Tully-Weaponizing-Data-Science-For-Social-Engineering-Automated-E2E-Spear-Phishing-On-Twitter-wp.pdf"
            ->  browser.attemptSpearphishingAttachment,
                browser.attemptSpearphishingLink

        | attemptSupplyChainCompromise @hidden [Bernoulli(0.70)*(Uniform(8,30)+TruncatedNormal(0.042,0.021))]
            developer info: "Sources for distribution result: https://savvyapps.com/blog/how-often-should-you-update-your-app#:~:text=Most%20successful%20apps%20release%201,updates%20with%20longer%20feature%20releases"
            ->  supplyChainCompromise

        & supplyChainCompromise 
            user info: "Supply chain compromise is the manipulation of products or product delivery mechanisms prior to receipt by a final consumer for the purpose of data or system compromise."
            ->  os.compromisedSystems

        & exploitationForClientExecution
            developer info: "Binary probability distribution."
            user info: "Adversaries can take advantage of certain vulnerabilities through targeted exploitation for the purpose of arbitrary code execution."
            ->  os.computer.user.attemptUserExecution

        | _exploitationForCredentialAccess @hidden
            ->  exploitationForCredentialAccess

        & exploitationForCredentialAccess
            user info: "Exploitation of a software vulnerability occurs when an adversary takes advantage of a programming error in a program, service, or within the operating system software or kernel itself to execute adversary-controlled code. Exploitation for credential access may also result in Privilege Escalation depending on the process targeted or credentials obtained."
            ->  exploitationOfSQLInjection,
                exploitationOfNTLMAuthentication         

        | exploitationOfSQLInjection [Bernoulli(0.14)*Exponential(0.746)]   
            developer info: "Sources used for distribution result: https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf, https://www.akamai.com/us/en/multimedia/documents/state-of-the-internet/soti-security-web-attacks-and-gaming-abuse-report-2019.pdf, https://trends.builtwith.com/websitelist/Akamai/Historical, https://www.imperva.com/docs/HII_Web_Application_Attack_Report_Ed6.pdf, https://www.cc.gatech.edu/fac/Alex.Orso/papers/halfond.viegas.orso.ISSSE06.pdf"
            ->  os.executeCode,
                exploitationForPrivilegeEscalation
                
        | exploitationOfNTLMAuthentication [Bernoulli(0.23346)*TruncatedNormal(0.011,0.00214)]
            developer info: "Sources used for distribution result: https://www.csoonline.com/article/3333916/i-can-get-and-crack-your-password-hashes-from-email.html, https://netmarketshare.com/operating-system-market-share.aspx?, https://resources.infosecinstitute.com/beyond-password-length-complexity/, https://spycloud.com/how-long-would-it-take-to-crack-your-password/, https://spycloud.com/2020-annual-credential-exposure-report/"
            ->  os.executeCode,
                exploitationForPrivilegeEscalation

        | _exploitationForDefenseEvasion @hidden
            ->  exploitationForDefenseEvasion

        & exploitationForDefenseEvasion
            ->  os.bypassAntivirus,
                os.bypassSystemAccessControls

        & attemptExploitationForPrivilegeEscalation @hidden
            ->  exploitationForPrivilegeEscalation

        | exploitationForPrivilegeEscalation
            user info: "This could enable someone to move from unprivileged or user level permissions to SYSTEM or root permissions depending on the component that is vulnerable."
            ->  os.adminAccount.adminRights,
                os.windows.sysAdmin.systemRights,
                thirdpartySoftware._useThirdpartySoftware

        | _exploitationOfRemoteServices
            user info: "An adversary may need to determine if the remote system is in a vulnerable state, which may be done through Network Service Scanning or other Discovery methods."
            ->  exploitationOfRemoteServices

        & exploitationOfRemoteServices
            ->  exploitationForPrivilegeEscalation,
                os.computer.infectedComputer
                
        | attemptExternalRemoteServices @hidden 
            ->  externalRemoteServices

        & externalRemoteServices [Bernoulli(0.19)*Exponential(3)]
            user info: "Adversaries may use remote services to initially access and/or persist within a network."
            developer info: "Sources for distribution result: https://www.securitymetrics.com/blog/remote-access-attacks-how-protect-against-malware, https://cdn2.hubspot.net/hubfs/4595665/Acunetix_web_application_vulnerability_report_2019.pdf, https://securitybrief.com.au/story/15-000-companies-still-critically-vulnerable-from-citrix-security-flaws-report"
            ->  os.computer.router.internalNetwork.persistence,
                os.computer.router.internalNetwork.remoteAccess,
                os.validAccounts,
                _exploitationOfRemoteServices

        # applicationIsolationAndSandboxing
            user info: "https://attack.mitre.org/mitigations/M1048/"
            ->  exploitationForClientExecution,
                exploitationForCredentialAccess,
                exploitationForDefenseEvasion,
                attemptExploitationForPrivilegeEscalation,
                exploitationOfRemoteServices,
                exploitHighVulnerabilityPublicFacingApplication,
                exploitMediumVulnerabilityPublicFacingApplication,
                exploitLowVulnerabilityPublicFacingApplication

        # applicationVetting
            user info: "https://attack.mitre.org/mitigations/M1005/"       
            ->  os.obfuscatedFilesOrInformation

        # encryptSensitiveInformation
            user info: "https://attack.mitre.org/mitigations/M1041/"
            ->  transmittedDataManipulation

        # executionPrevention
            user info: "https://attack.mitre.org/mitigations/M1038/"
            ->  remoteAccessTools

        # threatIntelligenceProgram
            user info: "https://attack.mitre.org/mitigations/M1019/"
            ->  exploitationForCredentialAccess,
                attemptExploitationForPrivilegeEscalation,
                exploitationOfRemoteServices  

        # updateSoftware [Bernoulli(0.34)]
            developer info: "Sources for distribution result: https://www.usenix.org/system/files/conference/soups2015/soups15-paper-ion.pdf, https://www.computerweekly.com/news/252462476/Most-businesses-vulnerable-to-supply-chain-cyber-attacks#:~:text=Seven%20in%2010%20businesses%20may,to%20research%20commissioned%20by%20Accenture"
            user info: "https://attack.mitre.org/mitigations/M1051/"
            ->  applicationDeploymentSoftware,
                exploitationForDefenseEvasion,
                attemptExploitationForPrivilegeEscalation,
                exploitHighVulnerabilityPublicFacingApplication,
                exploitMediumVulnerabilityPublicFacingApplication,
                exploitLowVulnerabilityPublicFacingApplication,
                supplyChainCompromise,
                os.computer.router.internalNetwork.remoteAccess,              
                os.windows.systemFirmware,
                os.windows.dLLSideLoading,
                os.service.thirdpartySoftware._useThirdpartySoftware                      
    }

    asset ThirdpartySoftware extends Service {  
        & applicationAccessToken
            user info: "Adversaries may use application access tokens to bypass the typical authentication process and access restricted accounts, information, or services on remote systems. These tokens are typically stolen from users and used in lieu of login credentials."
            ->  bypassLogonCredentials,
                bypassMultiFactorAuthentication

        | bypassLogonCredentials

        | bypassMultiFactorAuthentication

        & stealApplicationAccessToken
            user info: "Adversaries may gain access to and use third-party application deployment systems installed within an enterprise network." 
            ->  browser.spearphishingLink, //  send a link through Spearphishing Link to the target user to entice them to grant access to the application
                applicationAccessToken

        & trustedRelationship [Bernoulli(0.40)*TruncatedNormal(0.042,0.021)]
            user info: "Access through trusted third party relationship exploits an existing connection that may not be protected or receives less scrutiny than standard mechanisms of gaining access to a network." 
            developer info: "Sources for distribution result: accenture.com/_acnmedia/PDF-116/Accenture-Cybersecurity-Report-2020.pdf, https://enterprise.verizon.com/resources/reports/DBIR_2018_Report_execsummary.pdf" 
            ->  os._validAccounts // Valid Accounts used by the other party for access to internal network systems may be compromised

        & _useThirdpartySoftware @hidden [Bernoulli(0.70)]
            user info: "Adversaries may gain access to and use third-party application deployment systems installed within an enterprise network." 
            developer info: "Sources for distribution result: https://www.infoworld.com/article/2626167/third-party-code-putting-companies-at-risk.html, https://threatpost.com/third-party-applications-blame-87-percent-vulnerabilities-last-year-031513/77633/, https://www.helpnetsecurity.com/2018/11/20/third-party-risks/"
            ->  useThirdpartySoftware

        & useThirdpartySoftware
            ->  os.executeCode 
    }

    asset VideoCallApplication extends Service { 
        | _collectVideo @hidden
            ->  os.computer.collectVideo
    }
    
    asset Browser {
        & driveByCompromise [Bernoulli(0.37)]
            user info: "A drive-by compromise is when an adversary gains access to a system through a user visiting a website over the normal course of browsing." 
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/6657123"
            ->  malvertising,
                crossSiteScripting,
                service.os.computer.user.attemptUserExecution,
                service.exploitationForClientExecution

        | malvertising [Gamma(9,0.167)]
            developer info: "Sources for distribution result: https://www.adstage.io/resources/q1-2020-ppc-benchmark-report/; https://www.youtube.com/watch?v=rTg9l4d8MU4; https://www.facebook.com/business/help/204798856225114?id=649869995454285"
            ->  service.thirdpartySoftware.stealApplicationAccessToken

        | crossSiteScripting [Bernoulli(0.32)*Exponential(1)]
            developer info: "Sources for distribution result: https://books.google.fr/books?id=biGsBwAAQBAJ&pg=PA104&lpg=PA104&dq=forum+posts,+comments+compromise+browser+malware&source=bl&ots=rzJQ6XY73h&sig=ACfU3U2wC6Muu7MfakP6MHJLk7IXJ76C0A&hl=fr&sa=X&ved=2ahUKEwiomdS20dbpAhV65OAKHaD4Dz8Q6AEwAHoECAoQAQ#v=onepage&q=forum%20posts%2C%20comments%20compromise%20browser%20malware&f=false; http://www.ijiser.com/paper/2017/vol4issue8/Aug2017p101.1.pdf; https://www.acunetix.com/acunetix-web-application-vulnerability-report-2019/#xss"
            ->  service.thirdpartySoftware.stealApplicationAccessToken

        | executionThroughMaliciousContent

        & credentialsFromWebBrowsers [Bernoulli(0.625)]
            user info: "Adversaries may acquire credentials from web browsers by reading files specific to the target browser."
            developer info: "Sources for distribution result: https://link.springer.com/book/10.1007/978-3-319-94376-3, https://attack.mitre.org/techniques/T1503/, https://www.proofpoint.com/us/threat-insight/post/new-vega-stealer-shines-brightly-targeted-campaign"
            ->  service.os.userAccount.userCredentials
 
        | attemptSpearphishingLink @hidden      
            ->  spearphishingLink

        & spearphishingLink
            user info: "The links will be accompanied by social engineering text and require the user to actively click or copy and paste a URL into a browser, leveraging User Execution."
            ->  service.os.computer.user.spearphishingLinkClicked
            
        | attemptSpearphishingAttachment @hidden      
            ->  spearphishingAttachment
      
        & spearphishingAttachment
            user info: "Spearphishing attachment is a specific variant of spearphishing. Spearphishing attachment is different from other forms of spearphishing in that it employs the use of malware attached to an email." 
            ->  service.os.computer.user.spearphishingAttachmentDownload,
                service.os.windows.controlPanelItems // malicious Control Panel items can be delivered via Spearphishing Attachment campaigns

        | browserBookmark
            ->  browserBookmarkDiscovery

        | browserBookmarkDiscovery 
            ->  service.os.userAccount.userInformation,
                service.os.computer.router.internalNetwork.internalNetworkResourcesInformation

        | installExtensions
            ->  browserExtensions

        & browserExtensions [Bernoulli(0.85)]
            user info: "Once the extension is installed, it can browse to websites in the background, steal all information that a user enters into a browser, to include credentials, and be used as an installer for a RAT for persistence." 
            developer info: "Source used for distribution result: https://www.google.com/url?sa=t&rct=j&q=&esrc=s&source=web&cd=&cad=rja&uact=8&ved=2ahUKEwjQuta00ojqAhVGUZoKHT9YAygQFjAAegQIARAB&url=http%3A%2F%2Fmkto.cisco.com%2Frs%2F564-whv-323%2Fimages%2Fcisco-asr-2016.pdf&usg=AOvVaw1nLayTNlhdlV3tF_LfCxLf (Cisco 2016 Annual Security Report)"
            ->  service.os.userAccount.userInformation,
                service.os.userAccount.userCredentials,
                service.os.persistence

        | _websiteDefacement @hidden 
            user info: "This is a helper attack step that websites are a common victim of defacement. Website defacement may also be used as setup, or a precursor, for future attacks such as Drive-by Compromise."
            ->  driveByCompromise // Belongs to Drive-by Compromise attack

        & stealWebSessionCookie
            user info: "An adversary may steal web application or service session cookies and use them to gain access web applications or Internet services as an authenticated user without needing credentials."
            ->  webSessionCookie // after an adversary acquires a valid cookie, they can then perform a Web Session Cookie technique to login to the corresponding web application

        | webSessionCookie
            user info: "Adversaries can use stolen session cookies to authenticate to web applications and services. This technique bypasses some multi-factor authentication protocols since the session is already authenticated."
            ->  bypassLogonCredentials,
                bypassMultiFactorAuthentication

        | bypassLogonCredentials

        | bypassMultiFactorAuthentication   
            ->  service.externalRemoteServices,
                service.applicationDeploymentSoftware,
                service.userRemoteServicesLogin,
                service.adminRemoteServicesLogin,
                service.thirdpartySoftware.useThirdpartySoftware,
                service.os.windows.accountManipulation,
                service.os.bruteForce,
                service.os.adminAccount.createUserAccount,
                service.os.networkSniffing,
                service.os.validAccounts,
                service.os.windows.emailStorageCollection,
                service.os.windows.remoteDesktopProtocol

        # restrictWebBasedContent // Block unknown or unused attachments by default that should not be transmitted over email.
            user info: "https://attack.mitre.org/mitigations/M1021/"
            ->  spearphishingAttachment, 
                spearphishingLink,
                service.spearphishingViaService,
                driveByCompromise,
                service.os.domainGenerationAlgorithms,
                service.os.windows.compiledHTMLFile,
                service.os.computer.router.externalNetwork.webService
    }

    asset OS {   
        | accountAccessRemoval {A}
            user info: "Adversaries may interrupt availability of system and network resources by inhibiting access to accounts utilized by legitimate users. Accounts may be deleted, locked, or manipulated (ex: changed credentials) to remove access to accounts. Adversaries may also subsequently log off and/or reboot boxes to set malicious changes into place."

        | applicationExhaustionFlood
            ->  service.blockUserAccess 

        | infectedOS
            ->  clipboardData,
                dataFromInformationRepositories,
                computer.infectedComputer

        & clipboardData
            ->  adminAccount.adminCredentials,
                userAccount.userCredentials

        & accountDiscovery
            user info: "get account names and information"

        & domainDiscovery
            user info: "get domain names and information"

        | domainGenerationAlgorithms 
            user info: "Adversaries may make use of Domain Generation Algorithms (DGAs) to dynamically identify a destination for command and control traffic rather than relying on a list of static IP addresses or domains." 
            ->  fallbackChannels, // leads to Fallback Channels attack
                computer.router.internalNetwork.communicate,
                computer.router.internalNetwork.generateDomainNames

        | compromisedSystems // with probability
            ->  privateKeys,
                systemNetworkConnectionsDiscovery,
                service.browser.installExtensions,
                computer.peripheralDevice.removableMedia.infectedMedia,
                service.thirdpartySoftware.trustedRelationship

        & connectionProxy [Bernoulli(0.825)]
            user info: "Adversaries may use a connection proxy to direct network traffic between systems or act as an intermediary for network communications to a command and control server to avoid direct connections to their infrastructure."
            developer info: "Sources used for probability distribution: https://jpcertcc.github.io/ToolAnalysisResultSheet/details/Htran.htm#ExecCondition, https://gs.statcounter.com/os-market-share/desktop/worldwide/#monthly-201905-202005, https://netmarketshare.com/operating-system-market-share.aspx?, https://redcanary.com/threat-detection-report/techniques/powershell/, https://www.scribd.com/document/406643981/Threat-Detection-Report, https://s3.eu-west-1.amazonaws.com/ncsc-content/files/Joint%20report%20on%20publicly%20available%20hacking%20tools%20%28NCSC%29.pdf)"
            ->  bypassLogAnalysis,
                computer.router.internalNetwork.c2Connected,
                computer.router.internalNetwork.bypassFirewall,
                computer.router.externalNetwork.bypassFirewall

        | attemptBinaryPadding @hidden
            ->  binaryPadding

        & binaryPadding
            ->  bypassSignatureBasedDetection,
                bypassAntivirus

        & applicationWindowDiscovery
            user info: "get a listing of opened application windows"

        & processDiscovery [Bernoulli(0.17)*Exponential(346)]
            user info: "Adversaries may attempt to get information about running processes on a system. Information obtained could be used to gain an understanding of common software running on systems within the network."
            developer info: "Sources used for distribution result: https://content.carbonblack.com/c/vmwcb-threat-report-?x=f_lWMB, https://www.researchgate.net/publication/332380784_How_many_words_do_we_read_per_minute_A_review_and_meta-analysis_of_reading_rate, https://attack.mitre.org/techniques/T1057/"
            ->  processInjection

        | collectHashInformation
            ->  attemptBruteForce, // leads to Brute Force attack  
                windows.passTheHash, // leads to Pass the Hash attack
                windows.networkSniffing // leads to Network Sniffing attack

        & commonlyUsedPortsConnection
            user info: "Adversaries may communicate over a commonly used port to bypass firewalls or network detection systems and to blend with normal network activity to avoid more detailed inspection."
            ->  computer.router.internalNetwork.c2Connected,
                computer.router.internalNetwork.bypassNetworkIntrusionDetection,
                computer.router.firewall._bypassFirewall,
                computer.router.externalNetwork.bypassNetworkIntrusionDetection

        | compileAfterDelivery     
            user info: "Adversaries may attempt to make payloads difficult to discover and analyze by delivering files to victims as uncompiled code. Source code payloads may also be encrypted, encoded, and/or embedded within other files, such as those delivered as a Spearphishing Attachment."
            ->  service.browser.spearphishingAttachment,
                bypassStaticFileAnalysis,
                bypassBinaryAnalysis,
                bypassAntivirus,
                bypassHostIntrusionPrevention,
                bypassSignatureBasedDetection

        | attemptCredentialDumping @hidden
            ->  credentialDumping,
                systemOwnerOrUserDiscovery

        & credentialDumping
            user info: "Credential dumping is the process of obtaining account login and password information, normally in the form of a hash or a clear text password, from the operating system and software."
            ->  collectHashInformation

        | _credentialsInFiles @hidden
            ->  credentialsInFiles

        & credentialsInFiles
            ->  userAccount.userCredentials,
                adminAccount.adminCredentials,
                windows.sysAdmin.systemCredentials   

        | attemptCustomCryptographicProtocol @hidden
            ->  customCryptographicProtocol   

        & customCryptographicProtocol
            user info: "Adversaries may use a custom cryptographic protocol or algorithm to hide command and control traffic."
            ->  computer.router.internalNetwork.c2Connected  

        | attemptCustomCommandAndControlProtocol @hidden
            ->  customCommandAndControlProtocol

        & customCommandAndControlProtocol
            user info: "Adversaries may communicate using a custom command and control protocol instead of encapsulating commands/data in an existing Standard Application Layer Protocol."
            ->  computer.router.internalNetwork.c2Connected

        | _dataEncoding @hidden
            user info: "Use of data encoding may be to adhere to existing protocol specifications and includes use of ASCII, Unicode, Base64, MIME, UTF-8, or other binary-to-text and character encoding systems."
            ->  dataEncoding

        & dataEncoding [Bernoulli(0.0614)]
            developer info: "TTC instant/not applicable. Sources used for probability distribution result: https://symantec-enterprise-blogs.security.com/blogs/threat-intelligence/powershell-threats-grow-further-and-operate-plain-sight, https://docs.broadcom.com/docs/increased-use-of-powershell-in-attacks-16-en"
            ->  computer.router.internalNetwork.communicate
          
        & disablingSecurityTools [Bernoulli(0.23)*Exponential(111.55)]
            user info: "Adversaries may disable security tools to avoid possible detection of their tools and activities."
            developer info: "Sources for probability distribution: https://redcanary.com/threat-detection-report/techniques/disabling-security-tools/, https://labs.sentinelone.com/trickbot-update-brief-analysis-of-a-recent-trickbot-payload/, https://www.esentire.com/assets/resourcefiles/eSentire-2019-Annual-Threat-Intelligence-Report-2019-Perspectives-and-2020-Predictions.pdf, https://www.wilbursecurity.com/2020/03/trickbot-to-ryuk-in-two-hours/, https://www.trustwave.com/en-us/resources/blogs/spiderlabs-blog/tale-of-the-two-payloads-trickbot-and-nitol/"
            ->  bypassFileMonitoring,
                bypassHostIntrusionPrevention,
                bypassSignatureBasedDetection,
                bypassLogAnalysis,
                bypassAntivirus

        | domainFronting
            user info: "Domain fronting takes advantage of routing schemes in Content Delivery Networks (CDNs) and other services which host multiple domains to obfuscate the intended destination of HTTPS traffic or traffic tunneled through HTTPS."
            ->  computer.router.internalNetwork.obfuscateNetworkTraffic,
                computer.router.externalNetwork.obfuscateNetworkTraffic          
                 
        | executeCode 
            ->  computer.infectedComputer,
                computer.router.internalNetwork.remoteAccess

        | attemptEndpointDenialOfService @hidden {A}
            ->  endpointDenialOfService

        & endpointDenialOfService {A}
            user info: "Adversaries may perform Endpoint Denial of Service (DoS) attacks to degrade or block the availability of services to users."
            ->  applicationExhaustionFlood,
                osExhaustionFlood,
                windows.attemptServiceStop

        & executionGuardrails
            user info: "Adversaries may use guardrails and environmental keying to help protect their TTPs and evade detection."
            ->  bypassAntivirus, 
                bypassHostForensicAnalysis, 
                bypassSignatureBasedDetection,  
                bypassStaticFileAnalysis       

        | attemptExfiltrationOverOtherNetworkMedium @hidden
            ->  exfiltrationOverOtherNetworkMedium

        & exfiltrationOverOtherNetworkMedium
            user info: "Adversaries could choose to do exfiltration over a WiFi connection, modem, cellular data connection, Bluetooth, or another radio frequency (RF) channel if they have sufficient access or proximity, and the connection might not be secured or defended as well as the primary Internet-connected channel because it is not routed through the same enterprise network."     

        | fallbackChannels  
            user info: "Adversaries may use fallback or alternate communication channels if the primary channel is compromised or inaccessible in order to maintain reliable command and control and to avoid data transfer thresholds." 
            ->  computer.router.internalNetwork.communicate  

        & firmwareCorruption {A} [Bernoulli(0.8)*Exponential(2880)]
            user info: "Adversaries may overwrite or corrupt the flash memory contents of system BIOS or other firmware in devices attached to a system in order to render them inoperable or unable to boot." 
            developer info: "TTC assumed instant, one source states 30s. Sources used for probability distribution result: https://www.wired.com/2015/03/researchers-uncover-way-hack-bios-undermine-secure-operating-systems/, https://www.gillware.com/hard-drive-data-recovery/data-recovery-101-firmware/"

        | hiddenFilesAndDirectories
            user info: "To prevent normal users from accidentally changing special files on a system, most operating systems have the concept of a 'hidden' file. Adversaries can use this to their advantage to hide files and folders anywhere on the system for persistence and evading a typical user or system analysis that does not incorporate investigation of hidden files."
            ->  bypassHostForensicAnalysis,
                persistence

        | indicatorRemovalFromTools
            user info: "An adversary may be able to modify the tool by removing the indicator that is no longer detected by the target's defensive systems."
            ->  bypassHostIntrusionPrevention,
                bypassLogAnalysis,
                bypassAntivirus

        & indicatorRemovalOnHost
            user info: "Adversaries may delete or alter generated artifacts on a host system, including logs and potentially captured files such as quarantined malware."
            ->  bypassHostIntrusionPrevention,
                bypassLogAnalysis,
                bypassAntivirus    

        | inputCapture [Bernoulli(0.071)]
            user info: "Adversaries can use methods of capturing user input for obtaining credentials for Valid Accounts and information Collection that include keylogging and user input field interception."
            developer info: "Sources used for distribution result: https://www.statista.com/statistics/271037/distribution-of-most-common-malware-file-types/, https://www.rsaconference.com/industry-topics/blog/do-malware-authors-dream-of-original-code, http://keyllama.com/KeyLlamaUSB.pdf"
            ->  _validAccounts,
                userAccount.userCredentials 

        & installRootCertificate
            user info: "Installation of a root certificate on a compromised system would give an adversary a way to degrade the security of that system."
            ->  bypassDigitalCertificateValidation  

        | internalSpearphishing
            user info: "Adversaries may use internal spearphishing to gain access to additional information or exploit other users within the same organization after they already have access to accounts or systems within the environment." 
            ->  service.browser.spearphishingLink,
                service.browser.spearphishingAttachment,
                inputCapture

        & masquerading [Bernoulli(0.34)*Exponential(1)]
            user info: "Masquerading occurs when the name or location of an executable, legitimate or malicious, is manipulated or abused for the sake of evading defenses and observation."
            developer info: "TTC is time until attack is over, which is time until masquerading file is discovered. Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/masquerading/, https://www.tripwire.com/state-of-security/security-data-protection/security-controls/file-integrity-monitoring/, https://www.cm-alliance.com/cybersecurity-blog/understanding-how-file-integrity-monitoring-works, https://en.wikipedia.org/wiki/System_File_Checker"
            ->  bypassFileOrPathWhitelisting

        | attemptMultibandCommunication @hidden
            ->  multibandCommunication

        & multibandCommunication
            user info: "Some adversaries may split communications between different protocols. There could be one protocol for inbound command and control and another for outbound data, allowing it to bypass certain firewall restrictions."
            ->  computer.router.internalNetwork.c2Connected,
                computer.router.internalNetwork.bypassFirewall,
                computer.router.externalNetwork.bypassFirewall

        | attemptMultiHopProxy @hidden
            ->  multiHopProxy

        & multiHopProxy
            user info: "To disguise the source of malicious traffic, adversaries may chain together multiple proxies."
            ->  computer.router.internalNetwork.c2Connected

        | attemptMultilayerEncryption @hidden
            ->  multilayerEncryption

        & multilayerEncryption
            user info: "An adversary performs C2 communications using multiple layers of encryption, typically (but not exclusively) tunneling a custom encryption scheme within a protocol encryption scheme such as HTTPS or SMTPS."
            ->  computer.router.internalNetwork.c2Connected

        | attemptMultiStageChannels @hidden
            ->  multiStageChannels

        & multiStageChannels
            user info: "Adversaries may create multiple stages for command and control that are employed under different conditions or for certain functions."
            ->  computer.router.internalNetwork.c2Connected,
                fallbackChannels

        | attemptNetworkDenialOfService @hidden {A}
            ->  networkDenialOfService

        & networkDenialOfService {A} [Bernoulli(0.4863)*Exponential(6.6)]
            user info: "Adversaries may perform Network Denial of Service (DoS) attacks to degrade or block the availability of targeted resources to users."
            developer info: "Sources used for probability distribution: https://www.netscout.com/news/press-release/complexity-ddos-attacks, https://securelist.com/ddos-attacks-in-q4-2018/89565/, European Application and Network Security Report 2016-17, https://www.globaldots.com/ddos-distributed-denial-of-service-explained"
            ->  applicationExhaustionFlood,
                service.serviceExhaustionFlood
 
        & networkSniffing
            user info: "An adversary may place a network interface into promiscuous mode to passively access data in transit over the network, or use span ports to capture a larger amount of data."
            ->  userAccount.userCredentials,
                windows.lLMNR_NBT_NS_PoisoningAndRelay

        & obfuscatedFilesOrInformation [Bernoulli(0.7395)]
            user info: "Adversaries may use Obfuscated Files or Information to hide artifacts of an intrusion from analysis."     
            developer info: "TTC assumed instant. Sources for distribution result: https://www.recordedfuture.com/mitre-attack-tactics/, https://krisk.io/post/sok-attack-securecomm19.pdf, https://nakedsecurity.sophos.com/2010/07/27/large-piece-malware/"
            ->  windows.deobfuscateOrDecodeFilesOrInformation, // leads to Deobfuscate/Decode Files or Information attack
                bypassHostForensicAnalysis,
                bypassSignatureBasedDetection,
                bypassHostIntrusionPrevention,
                bypassApplicationWhitelisting,
                bypassProcessWhitelisting,
                bypassLogAnalysis,
                bypassFileOrPathWhitelisting      

        & privateKeys
            user info: "Adversaries may gather private keys from compromised systems for use in authenticating to Remote Services like SSH or for use in decrypting other collected files such as email. Private keys should require a password or passphrase for operation, so an adversary may also use Input Capture for keylogging or attempt to Brute Force the passphrase off-line."
            ->  service.userRemoteServicesLogin,
                inputCapture,
                bruteForce  

        | resourceHijacking {A}
            user info: "Adversaries may consume enough system resources to negatively impact and/or cause affected machines to become unresponsive."
            ->  computer.unresponsive  

        & communicationThroughRemovableMedia

        & remoteSystemDiscovery
            user info: "Adversaries will likely attempt to get a listing of other systems by IP address, hostname, or other logical identifier on a network that may be used for Lateral Movement from the current system."

        & detailedRemoteSystemDiscovery
            user info: "Adversaries with admin rights may get better details of the hosts enumerated"
        
        & adminCommandLineInterface
            developer info: "Binary probability distribution."
            ->  dataCollected, // entrypoints of attacks that can be done with command line interface under admin rights
                obfuscatedFilesOrInformation // obfuscate commands executed from payloads or directly via a Command-Line Interface 
        
        & userCommandLineInterface [Bernoulli(0.24)]
            developer info: "Sources for distribution results: https://www.itjungle.com/2020/06/22/security-gaining-attention-on-ibm-i-but-more-progress-needed/"
            ->  dataCollected, // entrypoints of attacks that can be done with command line interface under user rights
                obfuscatedFilesOrInformation // obfuscate commands executed from payloads or directly via a Command-Line Interface

        | fileAndDirectoryPermissionsModification [Bernoulli(0.2174)]
            user info: "File and directory permissions are commonly managed by discretionary access control lists (DACLs) specified by the file or directory owner."
            developer info: "Sources used for distribution result: https://attack.mitre.org/software/S0402, https://securelist.com/threats-to-macos-users/93116/, https://www.carbonblack.com/2019/02/12/tau-threat-intelligence-notification-new-macos-malware-variant-of-shlayer-osx-discovered/"
            ->  bypassFileSystemAccessControls,
                windows.accessibilityFeatures, // leads to Accessibility Features attack
                windows.logonScripts, //leads to Logon Scripts attack
                macOS.logonScripts // leads to Logon Scripts attack

        | graphicalUserInterface
            developer info: "Binary probability distribution."
            user info: "Adversaries may use a system's GUI during an operation, commonly through a remote interactive session such as Remote Desktop Protocol, to search for information and execute files via mouse double-click events, the Windows Run command, or other potentially difficult to monitor interactions."
            ->  windows.attemptSystemFirmware

        | softwareDiscovery
            user info: "Adversaries may attempt to get a listing of non-security related software that is installed on the system."

        | systemAccess

        & systemInformationDiscovery [Bernoulli(0.7133)]
            user info: "An adversary may attempt to get detailed information about the operating system and hardware, including version, patches, hotfixes, service packs, and architecture."
            developer info: "TTC assumed instant. sources used for distribution result: https://www.recordedfuture.com/mitre-attack-tactics/, https://krisk.io/post/sok-attack-securecomm19.pdf, https://attack.mitre.org/techniques/T1082/"
            ->  processInjection,
                systemNetworkConfigurationDiscovery,
                systemNetworkConnectionsDiscovery

        | systemNetworkConnectionsDiscovery
            user info: "Adversaries may attempt to get a listing of network connections to or from the compromised system they are currently accessing or from remote systems by querying for information over the network."

        | systemOwnerOrUserDiscovery
            user info: "Adversaries may attempt to identify the primary user, currently logged in user, set of users that commonly uses a system, or whether a user is actively using the system."
            ->  userAccount.userInformation

        | systemShutdownOrReboot {A}
            user info: "Adversaries may shutdown/reboot systems to interrupt access to, or aid in the destruction of, those systems."

        | osExhaustionFlood

        & processInjection [Bernoulli(0.35)*Exponential(127.1)] 
            user info: "Process injection is a method of executing arbitrary code in the address space of a separate live process. It can be used for Defense Evasion and Privilege Escalation."
            developer info: "Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/process-injection/, https://labs.sentinelone.com/trickbot-update-brief-analysis-of-a-recent-trickbot-payload/, https://www.wilbursecurity.com/2020/03/trickbot-to-ryuk-in-two-hours/, https://www.trustwave.com/en-us/resources/blogs/spiderlabs-blog/tale-of-the-two-payloads-trickbot-and-nitol/"
            ->  service.exploitationForPrivilegeEscalation,
                bypassAntivirus,
                bypassProcessWhitelisting

        & redundantAccess
            user info: "Adversaries may use more than one remote access tool with varying command and control protocols as a hedge against detection. Adversaries may also attempt to gain access to Valid Accounts to use External Remote Services such as external VPNs as a way to maintain access despite interruptions to remote access tools deployed within a target network."
            ->  _validAccounts,
                bypassAntivirus,
                computer.router.externalNetwork.bypassNetworkIntrusionDetection,
                computer.router.internalNetwork.bypassNetworkIntrusionDetection 

        | rootkit [Bernoulli(0.01255)]
            user info: "Adversaries may use rootkits to hide the presence of programs, files, network connections, services, drivers, and other system components."
            developer info: "Sources used for distribution: https://www.csoonline.com/article/3222066/how-to-detect-and-remove-a-rootkit-in-windows-10.html, https://investors.fireeye.com/static-files/56c2c6ec-3cdc-4fd2-967e-29205d2e982e, https://intezer.com/blog/linux/hiddenwasp-malware-targeting-linux-systems/"
            ->  modifyAPICalls, 
                bypassAntivirus,
                bypassFileMonitoring,
                bypassProcessWhitelisting,
                bypassSignatureBasedDetection, 
                bypassHostIntrusionPrevention,
                bypassSystemAccessControls,
                bypassFileOrPathWhitelisting,
                windows.hooking, // leads to hooking attack
                windows.hypervisor, // leads to hypervisor attack
                windows.attemptSystemFirmware // leads to system firmware attack

        & runtimeDataManipulation {I}
            user info: "By manipulating runtime data, adversaries may attempt to affect a business process, organizational understanding, and decision making."

        & standardCryptographicProtocol [TruncatedNormal(26.82,15.913)]
            user info: "Adversaries may explicitly employ a known encryption algorithm to conceal command and control traffic rather than relying on any inherent protections provided by a communication protocol."
            developer info: "TTC assumed instant. Sources for distribution result: https://krisk.io/post/sok-attack-securecomm19.pdf, https://www.recordedfuture.com/mitre-attack-tactics/, https://blogs.cisco.com/security/malwares-use-of-tls-and-encryption, https://news.sophos.com/en-us/2020/02/18/nearly-a-quarter-of-malware-now-communicates-using-tls/, https://www.cyren.com/blog/articles/over-one-third-of-malware-uses-https, https://istlsfastyet.com/, https://www.cloudflare.com/learning/ssl/what-happens-in-a-tls-handshake/"
            ->  computer.router.internalNetwork.c2Connected

        | attemptStandardApplicationLayerProtocol
            ->  standardApplicationLayerProtocol    

        & standardApplicationLayerProtocol [Bernoulli(0.20)]
            user info: "Adversaries may communicate using a common, standardized application layer protocol such as HTTP, HTTPS, SMTP, or DNS to avoid detection by blending in with existing traffic."
            developer info: "Sources used for distribution result: https://content.carbonblack.com/c/vmwcb-threat-report-?x=f_lWMB"
            ->  computer.router.internalNetwork.communicate

        | attemptStandardNonApplicationLayerProtocol
            ->  standardNonApplicationLayerProtocol    

        & standardNonApplicationLayerProtocol
            user info: "Use of a standard non-application layer protocol for communication between host and C2 server or among infected hosts within a network."
            ->  computer.router.internalNetwork.communicate        

        | systemNetworkConfigurationDiscovery
            user info: "Adversaries will likely look for details about the network configuration and settings of systems they access or through information discovery of remote systems."

        & twoFactorAuthenticationInterception
            user info: "Adversaries may target authentication mechanisms, such as smart cards, to gain access to systems, services, and network resources."
            ->  connectionProxy

        | modifyAPICalls                

        | bypassAntivirus

        | bypassAutorunsAnalysis

        | bypassApplicationWhitelisting

        | bypassBinaryAnalysis

        | bypassDefensiveNetworkServiceScanning

        | bypassDigitalCertificateValidation   

        | bypassFileMonitoring

        | bypassFileOrPathWhitelisting

        | bypassFileSystemAccessControls

        | bypassHeuristicDetection

        | bypassHostForensicAnalysis
        
        | bypassHostIntrusionPrevention

        | bypassProcessWhitelisting

        | bypassSystemAccessControls

        | bypassLogAnalysis        
        
        | bypassSignatureBasedDetection

        | bypassStaticFileAnalysis

        | bypassUserModeSignatureValidation
        
        | passwordPolicyDiscovery
            ->  bruteForceWithPasswordPolicy

        & bruteForceWithPasswordPolicy
            ->  userAccount.userCredentials // the probability distribution should be so that it has higher chance of success than the bruteforce attack

        & attemptBruteForce @hidden
            ->  bruteForce

        | bruteForce [Exponential(0.3)]
            user info: "Adversaries may use brute force techniques to attempt access to accounts when passwords are unknown or when password hashes are obtained."
            developer info: "Sources for distribution result: Filip Wilén and Andreas Westman. 'IoT-lang: Threat modeling for Internet of Things'. Unpublished paper."
            ->  userAccount.userCredentials           

        | attemptAntivirusCheck @hidden
            ->  antivirusCheck    

        & antivirusCheck
            ->  executeCode,
                service.exploitationForClientExecution

        | persistence

        & scripting [Bernoulli(0.38)*Exponential(1097)]
            user info: "Adversaries may use scripts to aid in operations and perform multiple actions that would otherwise be manual."
            developer info: "Sources used for probability result: https://redcanary.com/threat-detection-report/techniques/scripting/, https://www.labnol.org/code/20016-maximum-execution-time-limit, https://www.php.net/manual/en/function.set-time-limit.php, https://www.sitepoint.com/javascript-execution-browser-limits/"
            ->  _automatedCollection,
                service.browser.spearphishingAttachment, 
                service.exploitationForClientExecution,
                windows.virtualizationOrSandboxEvasion,
                windows.templateInjection

        | sSHCredentialInterception

        & storedDataManipulation {I}
            user info: "Adversaries may insert, delete, or manipulate data at rest in order to manipulate external outcomes or hide activity."    

        & uncommonlyUsedPortsConnection
            user info: "Adversaries may conduct C2 communications over a non-standard port to bypass proxies and firewalls that have been improperly configured."
            ->  computer.router.firewall._bypassFirewall,
                computer.router.internalNetwork.c2Connected,
                computer.router.internalNetwork.bypassProxies 

        | _validAccounts @hidden
            ->  validAccounts    

        & validAccounts
            developer info: "Binary probability relation."
            user info: "Adversaries may steal the credentials of a specific user or service account using Credential Access techniques or capture credentials earlier in their reconnaissance process through social engineering for means of gaining Initial Access."
            ->  service.userRemoteServicesLogin,
                service.adminRemoteServicesLogin,
                computer.router.internalNetwork.bypassFirewall,
                computer.router.internalNetwork.bypassNetworkIntrusionDetection,
                bypassHostIntrusionPrevention,
                bypassProcessWhitelisting,
                bypassSystemAccessControls,
                bypassAntivirus,
                graphicalUserInterface 

        | _credentialsInRegistry @hidden
            ->  credentialsInRegistry

        & credentialsInRegistry
            ->  userAccount.userCredentials,
                adminAccount.adminCredentials 

        & fileDeletion
            user info: "Adversaries may remove traces (files) over the course of an intrusion to keep their footprint low or remove them at the end as part of the post-intrusion cleanup process"
            ->  bypassHostForensicAnalysis

        | fileAndDirectoryDiscovery
            user info: "Adversaries may enumerate files and directories or may search in specific locations of a host or network share for certain information within a file system."
            ->  _automatedCollection

        & diskContentWipe {A} [Bernoulli(0.5433)*Exponential(126.437)]
            user info: "Adversaries may erase the contents of storage devices on specific systems as well as large numbers of systems in a network to interrupt availability to system and network resources."
            developer info: "Sources used for probability distribution: https://www.helpnetsecurity.com/2020/04/03/back-up-data/, https://threatpost.com/secrets-of-the-wiper-inside-the-worlds-most-destructive-malware/131836/, https://www.statista.com/statistics/795748/worldwide-seagate-average-hard-disk-drive-capacity/, https://hdd.userbenchmark.com/"
       
        & diskStructureWipe {A}
            user info: "Adversaries may corrupt or wipe the disk data structures on hard drive necessary to boot systems; targeting specific critical systems as well as a large number of systems in a network to interrupt availability to system and network resources."

        & remoteFileCopy [Bernoulli(0.29)]
            user info: "Files may be copied from one system to another to stage adversary tools or other files over the course of an operation."
            developer info: "TTC is very low (10-20 seconds) and is excluded. Sources used for probability result: https://redcanary.com/threat-detection-report/techniques/remote-file-copy/. For estimates on TTC please see remoteFileCopy.md in the Dropbox/MAL work/enterpriselang probability findings/folder."
            ->  windows.windowsAdminShares, // Adversaries may also copy files laterally between internal victim systems to support Lateral Movement with remote Execution using inherent file sharing protocols such as file sharing over SMB to connected network shares or with authenticated connections with Windows Admin Shares or Remote Desktop Protocol.
                windows.remoteDesktopProtocol,
                computer.user.attemptUserExecution, // leads to file execution attacks
                _automatedCollection // combined with Remote File Copy attack
        
        & sensitiveDataCollected
            ->  dataCompressed,
                computer.exfiltrationOverPhysicalMedium,
                dataSizedTransfer,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                automatedExfiltration,
                dataEncrypted, 
                dataStaged    

        & dataCollected
            ->  dataCompressed,
                computer.exfiltrationOverPhysicalMedium,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                dataEncrypted,
                dataStaged,
                automatedExfiltration,
                dataSizedTransfer

        | dataEncrypted
            user info: "Data is encrypted before being exfiltrated in order to hide the information that is being exfiltrated from detection or to make the exfiltration less conspicuous upon inspection by a defender."
            ->  computer.exfiltrationOverPhysicalMedium,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                automatedExfiltration

        | dataSizedTransfer
            user info: "An adversary may exfiltrate data in fixed size chunks instead of whole files or limit packet sizes below certain thresholds. This approach may be used to avoid triggering network data transfer threshold alerts."
            ->  computer.router.internalNetwork.dataExfiltration

        | dataStaged
            user info: "Collected data is staged in a central location or directory prior to Exfiltration. Data may be kept in separate files or combined into one file through techniques such as Data Compressed or Data Encrypted."
            ->  computer.exfiltrationOverPhysicalMedium, // leads to exfiltration attacks
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                dataCompressed,
                dataEncrypted
        
        & dataDestruction {A}
            ->  inhibitSystemRecovery // disable or delete system recovery features to augment the effects

        & dataEncryptedForImpact {A} [Bernoulli(0.366)*Exponential(12)]
            user info: "Adversaries may encrypt data on target systems or on large numbers of systems in a network to interrupt availability to system and network resources. To maximize impact on the target organization, malware designed for encrypting data may have worm-like features to propagate across a network by leveraging other attack techniques like Valid Accounts, Credential Dumping, and Windows Admin Shares."
            developer info: "Sources used for probability distribution result: https://www.google.com/url?sa=t&rct=j&q=&esrc=s&source=web&cd=&ved=2ahUKEwje19GOp5rqAhXnl4sKHSJbDFcQFjANegQIMBAB&url=https%3A%2F%2Fwww.checkpoint.com%2Fdownloads%2Fproducts%2Fransomware-defense-survey-for-enterprise.pdf&usg=AOvVaw1Om5gqIJ3-Gcf7efzgIby9, https://news.sophos.com/en-us/2020/05/12/the-state-of-ransomware-2020/, https://www.exabeam.com/library/anatomy-ransomware-attack/"
            ->  _validAccounts,
                attemptCredentialDumping,
                inhibitSystemRecovery,
                windows.windowsAdminShares

        & dataCompressed [Bernoulli(0.75)]
            developer info: "TTC assumed instant. For more info see enterpriselang probabilities/dataCompressed.md. Sources used for distribution: https://cloud.google.com/security/data-loss-prevention/preventing-data-exfiltration, https://targetedthreats.net/media/4-Glossary.pdf, https://www.wiley.com/en-se/The+Art+of+Memory+Forensics%3A+Detecting+Malware+and+Threats+in+Windows%2C+Linux%2C+and+Mac+Memory-p-9781118824993 pg. 560"
            ->  dataEncrypted,
                automatedExfiltration,
                computer.exfiltrationOverPhysicalMedium,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2

        & inhibitSystemRecovery {A}
            user info: "Adversaries may delete or remove built-in operating system data and turn off services designed to aid in the recovery of a corrupted system to prevent recovery."    
            ->  systemShutdownOrReboot

        | scheduledExfiltration
            ->  exfiltrationOverC2,
                exfiltrationOverAternativeProtocol       
    
        | exfiltrationOverAternativeProtocol
            ->  computer.router.internalNetwork.dataExfiltration

        | dataFromInformationRepositories 
            ->  service.informationRepositories
          
        | _automatedCollection @hidden
            ->  automatedCollection

        & automatedCollection
            user info: "Once established within a system or network, an adversary may use automated techniques for collecting internal data."
            ->  dataCollected, 
                sensitiveDataCollected

        | exfiltrationOverC2
            ->  computer.router.internalNetwork.dataExfiltration

        | automatedExfiltration
            ->  exfiltrationOverAternativeProtocol,
                exfiltrationOverC2

        & networkServiceScan
            user info: "Adversaries may attempt to get a listing of services running on remote hosts, including those that may be vulnerable to remote software exploitation."
            ->  service._exploitationOfRemoteServices

        & permissionGroupsDiscovery [Bernoulli(0.0526)]
            user info: "Adversaries may attempt to find local system or domain-level groups and permissions settings. "
            developer info: "Sources for probability result: Red Canary ATT&CK technique percentages (Top 50), https://www.fireeye.com/current-threats/annual-threat-report/mtrends.html, https://attack.mitre.org/techniques/T1069/"


        # antivirus [Bernoulli(0.53)]
            user info: "https://attack.mitre.org/mitigations/M1049/"
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/5665797; https://www.sans.org/reading-room/whitepapers/analyst/endpoint-protection-response-survey-38460; https://www.businesswire.com/news/home/20100804005348/en/Cyveillance-Testing-Finds-AV-Vendors-Detect-Average"
            ->  antivirusCheck,
                obfuscatedFilesOrInformation,
                service.browser.spearphishingAttachment,
                service.browser.spearphishingLink,
                service.spearphishingViaService   

        # restrictFileAndDirectoryPermissions 
            user info: "https://attack.mitre.org/mitigations/M1022/"
            ->  credentialsInFiles,
                disablingSecurityTools,
                indicatorRemovalOnHost,
                linux._clearCommandHistory,
                macOS._clearCommandHistory,
                masquerading,
                privateKeys,
                runtimeDataManipulation,
                storedDataManipulation,
                computer.router.externalNetwork.dataFromCloudStorageObject

        # accountUsePolicies // Set account lockout policies after a certain number of failed login attempts to prevent passwords from being guessed. Too strict a policy may create a denial of service condition and render environments un-usable, with all accounts used in the brute force being locked-out.
            user info: "https://attack.mitre.org/mitigations/M1036/" 
            developer info: "Binary probability relation."
            ->  attemptBruteForce

        # behaviorPreventionOnEndpoint
            user info: "https://attack.mitre.org/mitigations/M1040/"
            ->  processInjection

        # bootIntegrity 
            user info: "https://attack.mitre.org/mitigations/M1046/"
            ->  firmwareCorruption
 
        # dataBackup
            user info: "https://attack.mitre.org/mitigations/M1053/"
            ->  dataDestruction,
                dataEncryptedForImpact,
                inhibitSystemRecovery,
                diskContentWipe,
                diskStructureWipe,
                computer.router.internalNetwork.defacement,
                computer.router.externalNetwork.defacement

        # multiFactorAuthentication
            user info: "https://attack.mitre.org/mitigations/M1032/" 
            ->  attemptBruteForce,
                networkSniffing,
                validAccounts,
                service.browser.stealWebSessionCookie,
                service.applicationDeploymentSoftware,
                service.userRemoteServicesLogin,
                service.adminRemoteServicesLogin,
                service.thirdpartySoftware.useThirdpartySoftware

        # passwordPolicies 
            user info: "https://attack.mitre.org/mitigations/M1027/" 
            ->  attemptBruteForce,
                credentialDumping,
                credentialsInFiles,
                credentialsInRegistry,
                validAccounts,
                privateKeys,
                service.browser.credentialsFromWebBrowsers,
                service.thirdpartySoftware.useThirdpartySoftware

        # applicationIsolationAndSandboxing // Restrict execution of code to a virtual environment on or in transit to an endpoint system.
            user info: "https://attack.mitre.org/mitigations/M1048/"
            ->  scripting,
                service.browser.driveByCompromise

        # doNotMitigate // Execution Guardrails likely should not be mitigated with preventative controls because it may protect unintended targets from being compromised.
            user info: "https://attack.mitre.org/mitigations/M1055/"
            ->  executionGuardrails                

        # disableOrRemoveFeatureOrProgram 
            user info: "https://attack.mitre.org/techniques/T1064/"
            ->  scripting,
                communicationThroughRemovableMedia,
                service.exploitationOfRemoteServices,
                service.externalRemoteServices 

        # activeDirectoryConfiguration
            user info: "https://attack.mitre.org/mitigations/M1015/"
            ->  credentialDumping,
                credentialsInFiles,
                windows.sIDHistoryInjection, 
                service.thirdpartySoftware.useThirdpartySoftware

        # audit 
            user info: "https://attack.mitre.org/mitigations/M1047/"
            ->  credentialsInFiles,
                credentialsInRegistry,
                service.browser.browserExtensions,
                service.thirdpartySoftware.stealApplicationAccessToken,
                service.implantContainerImage,
                computer.router.externalNetwork.dataFromCloudStorageObject,
                privateKeys,
                validAccounts 

        # filterNetworkTraffic
            user info: "https://attack.mitre.org/mitigations/M1037/"
            ->  validAccounts

        # applicationDeveloperGuidance
            user info: "https://attack.mitre.org/mitigations/M1013/"
            ->  validAccounts

        # codeSigning [Bernoulli(0.28)]
            user info: "https://attack.mitre.org/mitigations/M1045/" 
            ->  service.applicationDeploymentSoftware,
                service.implantContainerImage,
                masquerading    

        # limitHardwareInstallation
            user info: "https://attack.mitre.org/mitigations/M1034/"
            developer info: "Binary probability relation."
            ->  computer.hardwareAddition.hardwareAdditions     

        # operatingSystemConfiguration 
            user info: "https://attack.mitre.org/mitigations/M1028/"
            ->  accountDiscovery, 
                communicationThroughRemovableMedia,
                credentialDumping,
                domainDiscovery,
                exfiltrationOverOtherNetworkMedium,
                inhibitSystemRecovery,
                installRootCertificate,
                adminAccount.createUserAccount

        # softwareConfiguration
            user info: "https://attack.mitre.org/mitigations/M1054/"
            ->  installRootCertificate,
                service.browser.stealWebSessionCookie      

        # executionPrevention // Block execution of code on a system through application whitelisting, blacklisting, and/or script blocking.            
            user info: "https://attack.mitre.org/mitigations/M1038/"
            ->  adminCommandLineInterface, 
                accountDiscovery,
                applicationWindowDiscovery,
                binaryPadding,
                service.browser.browserExtensions,
                computer.collectAudio,
                dataCompressed,
                detailedRemoteSystemDiscovery,
                domainDiscovery,
                domainFronting,
                fileDeletion,
                masquerading,
                permissionGroupsDiscovery,  
                processInjection,
                remoteSystemDiscovery,     
                systemInformationDiscovery,              
                userCommandLineInterface,
                userAccount.userExecution          
             
        # privilegedAccountManagement [Bernoulli(0.30)]
            developer info: "Sources for distribution results : https://lp.alpha-gen.co.uk/2018-global-state-of-pam/"
            user info: "https://attack.mitre.org/mitigations/M1026/"
            ->  credentialsInRegistry,
                validAccounts,
                service.implantContainerImage,
                computer.router.internalNetwork.webShell

        # encryptSensitiveInformation 
            user info: "https://attack.mitre.org/mitigations/M1041/"
            ->  sensitiveDataCollected,
                indicatorRemovalOnHost,
                networkSniffing,
                privateKeys,
                storedDataManipulation,
                computer.router.externalNetwork.dataFromCloudStorageObject  

        # remoteDataStorage 
            user info: "https://attack.mitre.org/mitigations/M1029/"
            ->  automatedCollection,
                indicatorRemovalOnHost,
                storedDataManipulation,
                service.thirdpartySoftware.useThirdpartySoftware              

        # limitSoftwareInstallation
            user info: "https://attack.mitre.org/mitigations/M1033/"
            ->  service.browser.browserExtensions

        # updateSoftware [Bernoulli(0.34)]
            user info: "https://attack.mitre.org/mitigations/M1051/"
            ->  firmwareCorruption,
                service.browser.driveByCompromise,
                service.exploitationForCredentialAccess,
                service.attemptExploitationForPrivilegeEscalation,
                service.exploitationOfRemoteServices,
                computer.router.internalNetwork.webShell  

        # userAccountControl
            user info: "https://attack.mitre.org/mitigations/M1052/"
            ->  service.thirdpartySoftware.trustedRelationship

        # exploitProtection
            user info: "https://attack.mitre.org/mitigations/M1050/"
            ->  service.browser.driveByCompromise,
                service.exploitationForClientExecution,
                service.exploitationForCredentialAccess,
                service.attemptExploitationForPrivilegeEscalation,
                service.exploitationOfRemoteServices,
                service.exploitHighVulnerabilityPublicFacingApplication,
                service.exploitMediumVulnerabilityPublicFacingApplication,
                service.exploitLowVulnerabilityPublicFacingApplication
    }
    
    asset SysAdmin {
        | systemRights
            user info: "A SYSTEM is a security principal. If the computer is joined to a domain, processes running as SYSTEM can access domain servers in the context of the computer's domain account. Processes running as Administrator have no access to domain computers."
            ->  windows._appCertDLLs,
                windows._bITSJobs,
                windows.changeDefaultFileAssociation,
                windows.componentObjectModelAndDistributedCOM,
                windows.controlPanelItems,
                windows._dLLSearchOrderHijacking,
                windows.executionThroughAPI,
                windows.extraWindowMemoryInjection,
                windows.hooking,
                windows.hypervisor,
                windows.imageFileExecutionOptionsInjection,
                windows.lSASS_Driver,
                windows.manInTheBrowser,
                windows.modifyExistingService,
                windows.modifyRegistry,
                windows.netshHelperDLL,
                windows.passwordFilterDLL,
                windows.pathInterception,
                windows.portMonitors,
                windows.processDoppelganging,
                windows.serverSoftwareComponent,
                windows.serviceExecution,
                windows.serviceRegistryPermissionsWeakness,
                windows.sIDHistoryInjection,
                windows.sIPAndTrustProviderHijacking,
                windows.systemServiceDiscovery,
                windows.serviceStop,
                windows.timeProviders,
                windows.timestomp,
                windows.windowsManagementInstrumentationEventSubscription,
                windows.winlogonHelperDLL,
                windows.os.accountAccessRemoval,
                windows.os.attemptCredentialDumping,
                windows.os._credentialsInFiles,
                windows.os.dataEncryptedForImpact,
                windows.os.fileAndDirectoryDiscovery,
                windows.os.fileAndDirectoryPermissionsModification,
                windows.os.firmwareCorruption,
                windows.os.graphicalUserInterface,
                windows.os.inhibitSystemRecovery,
                windows.os.inputCapture,
                windows.os.networkSniffing,
                windows.os.redundantAccess,
                windows.os.rootkit,
                windows.os.runtimeDataManipulation,
                windows.os.dataDestruction,
                windows.os.systemShutdownOrReboot,
                windows.os.storedDataManipulation,
                windows.os.twoFactorAuthenticationInterception,
                windows.os.service.thirdpartySoftware._useThirdpartySoftware

        | systemCredentials
            ->  systemRights
    }
    
    asset Windows extends OS {   
        & accountManipulation
            user info: "Account manipulation may aid adversaries in maintaining access to credentials and certain permission levels within an environment." 
            ->  userAccount.userCredentials,
                persistence

        | _appCertDLLs @hidden
            ->  appCertDLLs 
            
        & appCertDLLs
            user info: "This value can be abused to obtain persistence and privilege escalation by causing a malicious DLL to be loaded and run in the context of separate processes on the computer."
            ->  persistence,
                service.exploitationForPrivilegeEscalation

        & appInitDLLs
            ->  persistence,
                sysAdmin.systemRights

        & attemptAccessibilityFeatures @hidden
            ->  accessibilityFeatures

        | accessibilityFeatures
            user info: "This helps attackers to open a System-level command shell, where the attackers can execute arbitrary Windows commands, including adding or modifying accounts on the system."
            ->  persistence,
                sysAdmin.systemRights

        & _bITSJobs @hidden
            ->  bITSJobs

        & bITSJobs [Bernoulli(0.264)]
            user info: "Adversaries may abuse BITS to download, execute, and even clean up after running malicious code."
            developer info: "TTC assumed instant. Sources used for probability distribution: https://blog.trendmicro.com/trendlabs-security-intelligence/tropic-trooper-new-strategy/, https://gs.statcounter.com/os-version-market-share/windows/desktop/worldwide, https://netmarketshare.com/operating-system-market-share.aspx, https://en.wikipedia.org/wiki/Windows_Desktop_Gadgets, https://www.fireeye.com/blog/threat-research/2018/03/suspected-chinese-espionage-group-targeting-maritime-and-engineering-industries.html" 
            ->  executeCode,
                bypassHostForensicAnalysis,
                computer.router.firewall.bypassFirewall 

        & cmstp
            user info: "Microsoft Connection Manager Profile Installer (CMSTP.exe) is a command-line program used to install Connection Manager service profiles."
            developer info: "Binary probability distribution."
            ->  bypassUserAccountControl, // leads to Bypass User Account Control attack
                bypassApplicationWhitelisting,
                bypassAntivirus            

        | codeSigningCertificate [Bernoulli(0.72)*Exponential(96)]
            user info: "Adversaries are known to use code signing certificates to masquerade malware and tools as legitimate binaries. Code signing certificates may be used to bypass security policies that require signed code to execute on a system."
            developer info: "Sources for distribution results:https://www.venafi.com/blog/study-how-well-are-you-protecting-code-signing-certificates#:~:text=The%20study%20found%20that%20although,process%20for%20code%20signing%20certificates,https://duo.com/decipher/attackers-are-signing-malware-with-valid-certificates,https://cqureacademy.com/blog/windows-internals/code-signing"
            ->  bypassUserAccountControl

        & _componentObjectModelAndDistributedCOM @hidden [Exponential(96)]
            user info: "Adversaries may use the Windows Component Object Model (COM) and Distributed Component Object Model (DCOM) for local code execution or to execute on remote systems as part of lateral movement. By default, only Administrators may remotely activate and launch COM objects through DCOM."
            ->  componentObjectModelAndDistributedCOM

        & componentObjectModelAndDistributedCOM
            user info: "Adversaries may use the Windows Component Object Model (COM) and Distributed Component Object Model (DCOM) for local code execution or to execute on remote systems as part of lateral movement. By default, only Administrators may remotely activate and launch COM objects through DCOM."
            ->  computer.router.internalNetwork.remoteCOMExecution,
                dynamicDataExchange

        & controlPanelItems [Bernoulli(0.00006)]
            user info: "Adversaries can use Control Panel items as execution payloads to execute arbitrary commands."
            developer info: "TTC assumed instant, see mitreattacklang/enterpriselang probabilities/controlPanelItems.md for details. Sources used for probability distribution result: https://attack.mitre.org/software/S0172/, https://threatvector.cylance.com/en_us/home/reaver-mapping-connections-between-disparate-chinese-apt-groups.html, https://nvd.nist.gov/vuln/detail/CVE-2017-11882#vulnConfigurationsArea, https://unit42.paloaltonetworks.com/unit42-new-malware-with-ties-to-sunorcal-discovered/, https://community.spiceworks.com/software/articles/2873-data-snapshot-the-state-of-productivity-suites-in-the-workplace"
            ->  executeCode,
                bypassApplicationWhitelisting,
                bypassProcessWhitelisting

        | componentObjectModelHijacking
            user info: "Adversaries can use Component Object Model (COM) system to insert malicious code that can be executed. When that system component is executed through normal system operation the adversary's code will be executed instead."
            ->  processInjection,
                bypassAutorunsAnalysis,
                _regsvr32             

        | componentFirmware
            user info: "Adversaries may compromise computer components and install malicious firmware that will execute adversary code outside of the operating system and main system firmware or BIOS."       
            ->  bypassFileMonitoring,
                bypassHostIntrusionPrevention, 
                bypassAntivirus         

        & compiledHTMLFile [Bernoulli(0.93)*Exponential(1)]
            user info: "Adversaries may abuse this technology to conceal malicious code, can be triggered by User Execution."
            developer info: "Sources for distribution results : https://blog.rootshell.be/2017/12/19/malware-delivered-via-compiled-html-help-file/"
            ->  computer.user.attemptUserExecution, // leads to User Execution attack
                bypassApplicationWhitelisting,
                bypassDigitalCertificateValidation   

        | dCShadow
            user info: "This technique may bypass system logging and security monitors such as security information and event management (SIEM) products."
            ->  _sIDHistoryInjection,
                bypassLogAnalysis

        | deobfuscateOrDecodeFilesOrInformation 
            ->  bypassAntivirus,
                bypassHostIntrusionPrevention,
                bypassSignatureBasedDetection,
                computer.router.externalNetwork.bypassNetworkIntrusionDetection,
                computer.router.internalNetwork.bypassNetworkIntrusionDetection     

        | _dLLSearchOrderHijacking @hidden
            ->  dLLSearchOrderHijacking 

        & dLLSearchOrderHijacking [Bernoulli(0.16)]
            user info: "Adversaries may take advantage of the Windows DLL search order and programs that ambiguously specify DLLs to gain privilege escalation and persistence."
            developer info: "TTC is extremely low (< 0.00001 seconds) and is excluded. Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/dll-search-order-hijacking/. Sources for TTC (not used): http://blog.nsfocus.net/dridex-banking-malware-sample-technical-analysis-solution/, https://threatresearch.ext.hp.com/dridex-threat-analysis-july-2019-variant/"
            ->  bypassProcessWhitelisting,
                bypassUserAccountControl,
                userAccount.userRights,
                attemptlSASS_Driver,
                service.exploitationForPrivilegeEscalation                   
               
        | dLLSideLoading
            ->  bypassProcessWhitelisting,
                bypassAntivirus,
                attemptlSASS_Driver

        | attemptlSASS_Driver @hidden [Bernoulli(0.5)*Exponential(0.1)]
            -> lSASS_Driver

        & mshta [Bernoulli(0.3329)*Exponential(491)]
            user info: "Mshta is a utility that executes Microsoft HTML Applications. It can be used to proxy execute malicious .hta files, and can also be used to bypass application whitelisting."
            developer info: "Sources used for distribution: https://lolbas-project.github.io/lolbas/Binaries/Mshta/, https://netmarketshare.com/operating-system-market-share.aspx?, https://redcanary.com/blog/microsoft-html-application-hta-abuse-part-deux/"
            ->  executeCode,
                bypassDigitalCertificateValidation,
                bypassApplicationWhitelisting    
               
        | executeCode 
            +>  componentFirmware,
                service.exploitationForPrivilegeEscalation,
                computer.infectedWindowsComputer         

        & executionThroughAPI [Bernoulli(0.56)]
            user info: "Adversary tools may directly use the Windows application programming interface (API) to execute binaries." 
            developer info: "TTC assumed instant. Sources for probability distribution: https://krisk.io/post/sok-attack-securecomm19.pdf, https://www.recordedfuture.com/mitre-attack-tactics/, https://attack.mitre.org/techniques/T1106/"
            ->  executeCode,
                processInjection

        & executionThroughModuleLoad [Bernoulli(0.93)*Exponential(1)]
            developer info: "Sources for distribution results: Results on spearphishing and https://community.broadcom.com/symantecenterprise/communities/community-home/librarydocuments/viewdocument?DocumentKey=bc9e2e2e-028c-4ba1-863f-72a4700dbdc2&CommunityKey=1ecf5f55-9545-44d6-b0f4-4e4a7f5f5e68&tab=librarydocuments, https://www.cybereason.com/blog/information-stealing-malware-targeting-brazil-full-research"
            user info: "Adversaries can use this functionality as a way to execute arbitrary code on a system."
            ->  executeCode    
        
        | extraWindowMemoryInjection
            user info: "Execution granted through EWM injection may allow access to both the target process's memory and possibly elevated privileges."
            ->  processInjection

        | fileSystemLogicalOffsets
            user info: "Windows allows programs to have direct access to logical volumes. This technique bypasses Windows file access controls as well as file system monitoring tools."
            ->  bypassFileMonitoring,
                bypassFileSystemAccessControls

        | _fileSystemPermissionsWeakness @hidden
            ->  fileSystemPermissionsWeakness      

        & fileSystemPermissionsWeakness
            user info: "Adversaries may use this technique to replace legitimate binaries with malicious ones as a means of executing code at a higher permissions level."
            ->  executeCode,
                service.exploitationForPrivilegeEscalation

        & forcedAuthentication
            user info: "Adversaries may take advantage of this behavior to gain access to user account hashes through forced SMB authentication."
            ->  templateInjection,
                collectHashInformation 

        & groupPolicyModification
            user info: "Malicious GPO modifications can be used to implement Scheduled Task, Disabling Security Tools, Remote File Copy, Create Account, Service Execution and more."
            ->  scheduledTask, // leads to Scheduled Task attack
                disablingSecurityTools, // leads to Disabling Security Tools attack
                remoteFileCopy, // leads to Remote File Copy attack
                serviceExecution, // leads to Service Execution attack
                adminAccount.createUserAccount,
                bypassSystemAccessControls,
                bypassFileSystemAccessControls

        | imageFileExecutionOptionsInjection
            user info: "Directly modifying IEFO and silent process exit Registry values may be abused to obtain persistence and privilege escalation by causing a malicious executable to be loaded and run in the context of separate processes on the computer."
            ->  bypassAutorunsAnalysis,
                persistence,
                service.exploitationForPrivilegeEscalation

        | attemptIndicatorBlocking @hidden
            ->  indicatorBlocking

        & indicatorBlocking
            user info: "An adversary may attempt to block indicators or events typically captured by sensors from being gathered and analyzed."
            ->  bypassAntivirus,
                bypassLogAnalysis,
                bypassHostIntrusionPrevention     

        | indirectCommandExecution
            user info: "Adversaries may abuse the Windows system features for Defense Evasion."
            ->  executeCode,
                bypassStaticFileAnalysis,
                bypassApplicationWhitelisting,
                bypassProcessWhitelisting,
                bypassFileOrPathWhitelisting     

        & inputPrompt
            user info: "Adversaries may mimic this functionality to prompt users for credentials with a seemingly legitimate prompt for a number of reasons that mimic normal usage."
            developer info: "Binary probability distribution."
            ->  bypassUserAccountControl, // leads to Bypass User Account Control attack
                powerShell, // leads to PowerShell attack
                userAccount.userCredentials       

        & installUtil
            developer info: "Binary probability distribution."
            user info: "Adversaries may use InstallUtil to proxy execution of code through a trusted Windows utility."
            ->  bypassDigitalCertificateValidation,
                bypassProcessWhitelisting,
                signedBinaryProxyExecution,
                signedScriptProxyExecution    

        & kerberoasting
            user info: "Adversaries possessing a valid Kerberos ticket-granting ticket (TGT) may request one or more Kerberos ticket-granting service (TGS) service tickets for any SPN from a domain controller (DC)."
            ->  bruteForce,
                persistence,
                service.exploitationForPrivilegeEscalation,
                _validAccounts           

        & lLMNR_NBT_NS_PoisoningAndRelay
            user info: "AAdversaries can spoof an authoritative source for name resolution on a victim network by responding to LLMNR (UDP 5355)/NBT-NS (UDP 137) traffic. The adversary can then collect the hash information sent over the wire through tools that monitor the ports for traffic or through Network Sniffing and crack the hashes offline through Brute Force to obtain the plaintext passwords."
            ->  collectHashInformation

        & lSASS_Driver
            user info: "Adversaries may target lsass.exe drivers to obtain execution and/or persistence. By either replacing or adding illegitimate drivers (e.g., DLL Side-Loading or DLL Search Order Hijacking), an adversary can achieve arbitrary code execution triggered by continuous LSA operations."
            ->  executeCode,
                persistence

        & manInTheBrowser
            user info: "Adversaries can take advantage of security vulnerabilities and inherent functionality in browser software to change content, modify behavior, and intercept information as part of various man in the browser techniques."
            ->  service.browser.driveByCompromise

        & modifyExistingService
            user info: "Adversaries can modify an existing service to persist malware on a system by using system utilities or by using custom tools to interact with the Windows API. Adversaries may also intentionally corrupt or kill services to execute malicious recovery programs/commands."
            ->  persistence,
                masquerading, // Use of existing services is a type of Masquerading that may make detection analysis more challenging
                executeCode,
                serviceExecution

        & modifyRegistry
            user info: "Adversaries may interact with the Windows Registry to hide configuration information within Registry keys, remove information as part of cleaning up, or as part of other techniques to aid in Persistence and Execution. An adversary may modify these Registry keys to add new SSPs."
            ->  persistence,
                securitySupportProvider,
                bypassHostForensicAnalysis,
                componentObjectModelAndDistributedCOM   

        | netshHelperDLL
            user info: "Adversaries can use netsh.exe with helper DLLs to proxy execution of arbitrary code in a persistent manner."

        | networkShareConnectionRemoval
            user info: "Adversaries may remove share connections that are no longer useful in order to clean up traces of their operation."
            ->  bypassHostForensicAnalysis

        & newService
            user info: "Adversaries may install a new service that can be configured to execute at startup by using utilities to interact with services or by directly modifying the Registry."
            ->  service.exploitationForPrivilegeEscalation, // an adversary may also use a service to escalate privileges from administrator to SYSTEM
                masquerading // benign software with Masquerading

        | attemptNtfsFileAttributes @hidden
            ->  ntfsFileAttributes

        & ntfsFileAttributes
            user info: "Adversaries may store malicious data or binaries in file attribute metadata instead of directly in files, to evade some defenses, such as static indicator scanning tools and anti-virus."
            ->  bypassSignatureBasedDetection,
                bypassHostForensicAnalysis,
                bypassAntivirus

        & officeApplicationStartup
            user info: "There are multiple mechanisms that can be used with Office for persistence when an Office-based application is started."
            ->  persistence  

        & passwordFilterDLL
            user info: "Adversaries can register malicious password filters to harvest credentials from local computers and/or entire domains."
            ->  sysAdmin.systemCredentials,
                adminAccount.windowsAdmin.adminCredentials

        & passTheHash
            user info: "This method bypasses standard authentication steps that require a cleartext password, moving directly into the portion of the authentication that uses the password hash."
            ->  computer.router.internalNetwork.remoteSystemsConnection // PtH may be used to perform actions on local or remote systems

        | attemptPassTheTicket @hidden
            ->  passTheTicket

        & passTheTicket
            user info: "Pass the ticket (PtT) is a method of authenticating to a system using Kerberos tickets without having access to an account's password."
            ->  attemptCredentialDumping, // valid Kerberos tickets for Valid Accounts are captured by Credential Dumping
                computer.router.internalNetwork.c2Connexion // gain remote access 

        & pathInterception [Bernoulli(0.0352)]
            user info: "Adversaries may take advantage of performing path interception: unquoted paths, path environment variable misconfigurations, and search order hijacking."
            developer info: "Sources used for distribution result: https://content.fireeye.com/m-trends/rpt-m-trends-2020, http://www.hexacorn.com/blog/2016/03/26/beyond-good-ol-run-key-part-37/"
            ->  unquotedPaths,
                pathEnvironmentVariableMisconfiguration,
                searchOrderHijacking

        | parentPIDSpoofing
            user info: "Adversaries may spoof the parent process identifier (PPID) of a new process to evade process-monitoring defenses or to elevate privileges."
            ->  bypassHostForensicAnalysis,
                bypassHeuristicDetection,
                service.exploitationForPrivilegeEscalation

        | pathEnvironmentVariableMisconfiguration 
            user info: "A program may be placed in the preceding directory that is named the same as a Windows program (such as cmd, PowerShell, or Python), which will be executed when that command is executed from a script or command-line."
            ->  executeCode

        | attemptPowerShell @hidden
            user info: "Adversaries can use PowerShell to perform a number of actions, including discovery of information and execution of code."
            ->  powerShell,
                windowsRemoteManagement, // call with the winrm
                computer.user.attemptUserExecution,
                scripting

        & powerShell [Bernoulli(0.55)]
            user info: "Administrator permissions are required to use PowerShell to connect to remote systems."    
            developer info: "The process takes a few seconds to complete, so the TTC has excluded. It would be [Exponential(28800)] if included. Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/powershell/"  
            ->  computer.router.internalNetwork.remoteSystemsConnection,
                os.computer.router.externalNetwork.attemptTransmittedDataManipulation,
                persistence

        & _powerShellProfile @hidden
            user info: "Adversaries may gain persistence and elevate privileges in certain situations by abusing PowerShell profiles."
            ->  powerShellProfile

        | powerShellProfile
            user info: "Adversaries may gain persistence and elevate privileges in certain situations by abusing PowerShell profiles."   
            ->  service.exploitationForPrivilegeEscalation,
                persistence

        | processDoppelganging
            user info: "Process Doppelgänging involves replacing the memory of a legitimate process, enabling the veiled execution of malicious code that may evade defenses and detection."
            ->  bypassAntivirus,
                bypassProcessWhitelisting,
                bypassSignatureBasedDetection,
                bypassFileOrPathWhitelisting    

        | processHollowing
            user info: "Execution of the malicious code is masked under a legitimate process and may evade defenses and detection analysis."
            ->  bypassAntivirus,
                bypassProcessWhitelisting,
                bypassSignatureBasedDetection,
                bypassFileOrPathWhitelisting   

        & codeProxyExecution
            ->  persistence

        | rDPSessionHijacking
            user info: "Adversaries may also perform RDP session hijacking which involves stealing a legitimate user's remote session."

        | registryRunKeysOrStartupFolder [Bernoulli(0.17125)]
            user info: "Adversaries can use these configuration locations to execute malware, such as remote access tools, to maintain persistence through system reboots. Adversaries may also use Masquerading to make the Registry entries look as if they are associated with legitimate programs."
            developer info: "Sources used for distribution result: https://dsimg.ubm-us.net/envelope/395823/551993/Fileless%20Attack%20Survival%20Guide.pdf, https://content.carbonblack.com/c/vmwcb-threat-report-?x=f_lWMB"
            ->  masquerading, // leads to Masquerading attack
                service.remoteAccessTools,
                persistence

        & regsvcsOrRegasm
            developer info: "Binary probability distribution."
            user info: "Adversaries can use Regsvcs and Regasm to proxy execution of code through a trusted Windows utility. Both utilities may be used to bypass process whitelisting."
            ->  codeProxyExecution, 
                bypassProcessWhitelisting,
                bypassDigitalCertificateValidation

        & _regsvr32 @hidden
            developer info: "This is a helper attack step for Regsvr32.exe to establish Persistence via Component Object Model Hijacking."
            ->  persistence

        & regsvr32
            developer info: "Binary probability distribution."
            user info: "Adversaries may take advantage of this functionality to proxy execution of code to avoid triggering security tools. Regsvr32.exe can also be used to specifically bypass process whitelisting using functionality to load COM scriptlets to execute DLLs under user permissions."
            ->  bypassProcessWhitelisting,
                bypassAntivirus,
                bypassDigitalCertificateValidation,
                codeProxyExecution,
                _regsvr32

        & remoteDesktopProtocol [Bernoulli(0.5561)*Exponential(480)]
            user info: "Adversaries may connect to a remote system over RDP/RDS to expand access if the service is enabled and allows access to accounts with known credentials. Adversaries may also perform RDP session hijacking which involves stealing a legitimate user's remote session. It can also lead to Remote System Discovery and Privilege Escalation by stealing a Domain Admin or higher privileged account session."
            developer info: "Sources used for distribution result: https://www.fireeye.com/blog/threat-research/2020/02/mtrends-2020-insights-from-the-front-lines.html, https://duo.com/decipher/attackers-combine-attacks-against-rdp-with-ransomware, https://www.securitymagazine.com/articles/90983-percent-of-organizations-exhibit-malicious-remote-desktop-protocol-rdp-behaviors, https://medium.com/enterprise-nxt/2020-ransomware-attacks-still-mostly-through-unsecured-rdp-b167fefcd490, https://www.youtube.com/watch?v=Olgso_oXQa8"
            ->  accessibilityFeatures,
                remoteSystemDiscovery,
                rDPSessionHijacking,
                graphicalUserInterface

        & replicationThroughRemovableMedia [Bernoulli(0.45)*Exponential(3.429)]
            user info: "Adversaries may move onto systems, possibly those on disconnected or air-gapped networks, by copying malware to removable media and taking advantage of Autorun features when the media is inserted into a system and executes."
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/7546509, https://threatpost.com/never-trust-a-found-usb-drive-black-hat-demo-shows-why/119653/"
            ->  communicationThroughRemovableMedia,
                computer.peripheralDevice.removableMedia.infectedMedia

        & rundll32
            developer info: "Binary probability distribution."
            user info: "Adversaries may take advantage of this functionality to proxy execution of code to avoid triggering security tools. Rundll32 can also been used to execute scripts."
            ->  bypassAntivirus,
                bypassApplicationWhitelisting,
                bypassDigitalCertificateValidation,
                executeCode  

        & serverSoftwareComponent
            user info: "Adversaries may abuse legitimate extensible development features of server applications to establish persistent access to systems."
            ->  persistence

        & screensaver
            user info: "Adversaries can use screensaver settings to maintain persistence by setting the screensaver to run malware after a certain timeframe of user inactivity."
            ->  persistence

        | searchOrderHijacking
            user info: "Search order hijacking occurs when an adversary abuses the order in which Windows searches for programs that are not given a path."
            ->  _dLLSearchOrderHijacking // Search order hijacking is a common practice for DLL Search Order Hijacking   

        & securitySupportProvider
            user info: "An adversary may modify these Registry keys to add new SSPs."
            ->  persistence

        & serviceExecution
            developer info: "Binary probability distribution."
            user info: "Adversaries may execute a binary, command, or script via a method that interacts with Windows services, such as the Service Control Manager."
            ->  newService // Adversaries may also directly start services through Service Execution

        | serviceRegistryPermissionsWeakness
            ->  _serviceRegistryPermissionsWeakness,
                persistence

        & _serviceRegistryPermissionsWeakness @hidden
            user info: "When the service starts or is restarted, then the adversary-controlled program will execute, allowing the adversary to gain persistence and/or privilege escalation to the account context the service is set to execute."
            ->  service.exploitationForPrivilegeEscalation

        | attemptSharedWebroot @hidden
            ->  sharedWebroot

        & sharedWebroot
            user info: "Adversaries may add malicious content to an internally accessible website through an open network file share that contains the website's webroot or Web content directory and then browse to that content with a Web browser to cause the server to execute the malicious content."
            ->  service.browser.executionThroughMaliciousContent

        & shortcutModification
            user info: "Adversaries could use shortcuts to execute their tools for persistence. Adversaries could also edit the target path or entirely replace an existing shortcut so their tools will be executed instead of the intended legitimate program."
            ->  executeCode,
                persistence,
                masquerading // create a new shortcut as a means of indirection that may use Masquerading to look like a legitimate program

        & sIDHistoryInjection
            user info: "Adversaries may use SID mechanism for privilege escalation. This manipulation may result in elevated access to local resources and/or access to otherwise inaccessible domains via lateral movement techniques such as Remote Services, Windows Admin Shares, or Windows Remote Management."
            ->  _sIDHistoryInjection,
                service.adminRemoteServicesLogin, // leads to Remote Services attack
                windowsAdminShares, // leads to Windows Admin Shares attack    
                windowsRemoteManagement // leads to Windows Remote Management attack 

        & _sIDHistoryInjection @hidden
            user info: "This is a helper attack step. Adversaries may utilize DCShadow to perform SID-History Injection and/or manipulate AD objects (such as accounts, access control lists, schemas) to establish backdoors for Persistence."           
            ->  persistence          

        & signedBinaryProxyExecution
            user info: "Several Microsoft signed binaries that are default on Windows installations can be used to proxy execution of other files. This behavior may be abused by adversaries to execute malicious files that could bypass application whitelisting and signature validation on systems."
            ->  bypassApplicationWhitelisting,
                bypassDigitalCertificateValidation            

        & signedScriptProxyExecution
            user info: "Scripts signed with trusted certificates can be used to proxy execution of malicious files."
            ->  bypassApplicationWhitelisting,
                bypassDigitalCertificateValidation          

        & sIPAndTrustProviderHijacking
            user info: "Adversaries may abuse this architecture to subvert trust controls and bypass security policies that allow only legitimately signed code to execute on a system."
            ->  bypassApplicationWhitelisting,
                bypassAutorunsAnalysis,
                bypassDigitalCertificateValidation,
                bypassProcessWhitelisting,
                bypassUserModeSignatureValidation 

        | attemptSoftwarePacking @hidden
            ->  softwarePacking

        & softwarePacking [TruncatedNormal(0.56784,0.25219)]
            user info: "Packing an executable changes the file signature in an attempt to avoid signature-based detection."
            developer info: "Sources for distribution result: https://www.carbonblack.com/resources/threat-research/cybersecurity-outlook-report/#download, https://attack.mitre.org/techniques/T1045/, http://www.matcode.com/mpress.htm, https://upx.github.io/, https://d3gpjj9d20n0p3.cloudfront.net/fortiguard/research/DetectingMalwareThreats.pdf, https://www.researchgate.net/publication/301902892_Entropy_analysis_to_classify_unknown_packing_algorithms_for_malware_detection, https://www.academia.edu/7383312/Obfuscation_The_Hidden_Malware, https://www.lastline.com/labsblog/malware-packing/, www.blackhat.com/presentations/bh-usa-06/BH-US-06-Morgenstern.pdf"
            ->  indicatorRemovalFromTools,
                bypassSignatureBasedDetection,
                bypassAntivirus,
                bypassHeuristicDetection  

        | systemServiceDiscovery
            user info: "Adversaries may try to get information about registered services."
            ->  service.serviceInformation 

        | _taintSharedContent @hidden
            user info: "This is a helper attack step that requires access to shared folders."
            ->  computer.router.internalNetwork.networkSharedDrive.taintSharedContent   

        & templateInjection
            user info: "Adversaries may abuse this technology to initially conceal malicious code to be executed."
            ->  bypassStaticFileAnalysis, 
                forcedAuthentication, // leads to Forced Authentication attack
                service.browser.spearphishingAttachment, // can be delivered via Spearphishing Attachment
                _taintSharedContent // can be delivered via Taint Shared Content

        & timeProviders
            user info: "Adversaries may abuse Windows Time service to establish Persistence, specifically by registering and enabling a malicious DLL as a time provider."
            ->  persistence

        & trustedDeveloperUtilities
            developer info: "Binary probability distribution."
            user info: "An adversary can use tracker.exe to proxy execution of an arbitrary DLL into another process. Since tracker.exe is also signed it can be used to bypass application whitelisting solutions."
            ->  bypassApplicationWhitelisting,
                executeCode,
                signedBinaryProxyExecution,
                signedScriptProxyExecution    

        | unquotedPaths
            user info: "An adversary can place an executable in a higher level directory of the path, and Windows will resolve that executable instead of the intended executable."
            ->  executeCode   

        | attemptServiceStop @hidden {A}
            ->  serviceStop

        & serviceStop {A}
            user info: "Adversaries may stop services in order to conduct Data Destruction or Data Encrypted for Impact on the data stores of services like Exchange and SQL Server."
            ->  dataDestruction,
                dataEncryptedForImpact

        & scheduledTask [Bernoulli(0.33)*Exponential(54.3)]
            user info: "An adversary may use task scheduling to execute programs at system startup or on a scheduled basis for persistence, to conduct remote Execution as part of Lateral Movement, to gain SYSTEM privileges, or to run a process under the context of a specified account."
            developer info: "Sources used for probability results: https://docs.microsoft.com/en-us/previous-versions/windows/it-pro/windows-xp/bb490866(v=technet.10)?redirectedfrom=MSDN, https://redcanary.com/threat-detection-report/techniques/scheduled-task/, https://labs.sentinelone.com/trickbot-update-brief-analysis-of-a-recent-trickbot-payload/, https://www.wilbursecurity.com/2020/03/trickbot-to-ryuk-in-two-hours/, https://www.trustwave.com/en-us/resources/blogs/spiderlabs-blog/tale-of-the-two-payloads-trickbot-and-nitol/, https://www.esentire.com/assets/resourcefiles/eSentire-2019-Annual-Threat-Intelligence-Report-2019-Perspectives-and-2020-Predictions.pdf, https://blog.malwarebytes.com/cybercrime/2015/03/scheduled-tasks/, https://www.zdnet.com/article/microsoft-says-new-dexphot-malware-infected-more-than-80000-computers/"
            ->  executeCode,
                persistence

        & remoteScheduleTask
            ->  computer.router.internalNetwork.remoteExecution

        | attemptSystemTimeDiscovery @hidden
            ->  systemTimeDiscovery

        & systemTimeDiscovery
            user info: "The information could be useful for performing other techniques, such as executing a file with a Scheduled Task, or to discover locality information based on time zone to assist in victim targeting."
            ->  scheduledTask         

        & windowsAdminShares [Bernoulli(0.28)*Exponential(4114)]
            developer info: "Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/windows-admin-shares/, https://www.contextis.com/us/blog/lateral-movement-a-deep-look-into-psexec"
            ->  networkShareConnectionRemoval, // leads to Network Share Connection Removal attack
                windowsManagementInstrumentation // leads to Windows Management Instrumentation attack

        | windowsManagementInstrumentation [Bernoulli(0.26)*Exponential(43.49)]
            user info: "An adversary can use WMI to interact with local and remote systems and use it as a means to perform many tactic functions."
            developer info: "TTC is upper bound of likely time needed. Sources used for probability distribution result: https://redcanary.com/threat-detection-report/techniques/windows-management-instrumentation/, https://www.fireeye.com/content/dam/fireeye-www/global/en/current-threats/pdfs/wp-windows-management-instrumentation.pdf, https://www.microsoft.com/security/blog/2017/06/27/new-ransomware-old-techniques-petya-adds-worm-capabilities/, https://securelist.com/olympicdestroyer-is-here-to-trick-the-industry/84295/"
            ->  systemInformationDiscovery, // extract information about the operating system
                executeCode,
                inhibitSystemRecovery

        & windowsManagementInstrumentationEventSubscription
            user info: "Adversaries may use the capabilities of WMI to subscribe to an event and execute arbitrary code when that event occurs, providing persistence on a system."
            ->  persistence

        & windowsRemoteManagement
            user info: "WinRM allows a user to interact with a remote system."
            ->  computer.router.internalNetwork.remoteExecution,
                service.externalRemoteServices // Windows Remote Management can also be used externally  

        & winlogonHelperDLL
            user info: "Adversaries may take advantage of these features to repeatedly execute malicious code and establish Persistence."
            ->  persistence

        | attemptRegistryKeysEnabled @hidden
            ->  registryKeysEnabled

        & registryKeysEnabled
            ->  dynamicDataExchange

        | dynamicDataExchange [Bernoulli(0.93)*Exponential(1)]
            user info: "Windows Dynamic Data Exchange (DDE) is a client-server protocol for one-time and/or continuous inter-process communication (IPC) between applications."
            ->  userCommandLineInterface,
                executeCode 
            
        | hooking             
            user info: "Adversaries may use hooking to load and execute malicious code within the context of another process; malicious hooking mechanisms may also capture API calls that include parameters that reveal user authentication credentials for Credential Access."
            ->  executeCode,
                captureAPICalls,
                service.exploitationForPrivilegeEscalation

        | hypervisor           
            user info: "A type-1 hypervisor operates at a level below the operating system and could be designed with Rootkit functionality to hide its existence from the guest operating system."
            ->  persistence

        | captureAPICalls
            user info: "Some API calls include parameters that reveal user authentication credentials for Credential Access."
            -> userAccount.userCredentials

        & logonScripts [Exponential(288)]
            user info: "An adversary can then create a logon session for the user using the LogonUser function. The function will return a copy of the new session's access token and the adversary can use SetThreadToken to assign the token to a thread."
            developer info: "Sources for probability distribution: http://www.hexacorn.com/blog/2014/11/14/beyond-good-ol-run-key-part-18/, "
            ->  accessTokenManipulation,
                adminAccount.windowsAdmin.adminRights   

        | attemptSystemFirmware @hidden
            ->  systemFirmware
        
        & systemFirmware
            user info: "System firmware may be modified by an adversary to perform or assist in malicious activity."
            ->  systemShutdownOrReboot

        | systemShutdownOrReboot {A}
            user info: "Adversaries may shutdown/reboot systems to interrupt access to, or aid in the destruction of, those systems."  

        | timestomp
            user info: "Timestomp is done on files that have been modified or created by the adversary so that they do not appear conspicuous to forensic investigators or file analysis tools."
            ->  bypassHostForensicAnalysis

        & queryRegistery
            user info: "The Registry contains a significant amount of information about the operating system, configuration, software, and security. Some of the information may help adversaries to further their operation within a network."
            ->  scripting,
                systemInformationDiscovery

        & networkShareDiscovery
            ->  computer.router.internalNetwork.networkShareDiscovery

        & attemptAccessTokenManipulation @hidden
            ->  accessTokenManipulation

        | accessTokenManipulation [Bernoulli(0.20715)]
            user info: "Adversaries may use access tokens to operate under a different user or system security context to perform actions and evade detection."
            developer info: "Sources for probability distribution result: https://krisk.io/post/sok-attack-securecomm19.pdf, https://pentestlab.blog/2017/04/03/token-manipulation/"
            ->  tokenImpersonationOrTheft,
                createProcessWithAToken,
                makeAndImpersonateToken

        | tokenImpersonationOrTheft
            ->  service.exploitationForPrivilegeEscalation
        
        | createProcessWithAToken 
            ->  service.exploitationForPrivilegeEscalation

        | makeAndImpersonateToken
            ->  service.exploitationForPrivilegeEscalation

        & securitySoftwareDiscovery [Bernoulli(0.83387)]
            user info: "Adversaries may attempt to get a listing of security software, configurations, defensive tools, and sensors that are installed on the system. This may include things such as local firewall rules and anti-virus. These checks may be built into early-stage remote access tools."
            developer info: "TTC assumed instant. Sources used for probability distribution result: https://krisk.io/post/sok-attack-securecomm19.pdf, https://www.recordedfuture.com/mitre-attack-tactics/, https://attack.mitre.org/techniques/T1063/"
            ->  virtualizationOrSandboxEvasion

        & bypassUserAccountControl [Bernoulli(0.277)]
            developer info: "TTC assumed instant. Sources for probability distribution: https://attack.mitre.org/software/S0129/, https://www.forcepoint.com/sites/default/files/resources/files/forcepoint-security-labs-monsoon-analysis-report.pdf, https://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2014-6352, https://gs.statcounter.com/os-version-market-share/windows/desktop/worldwide, https://netmarketshare.com/operating-system-market-share.aspx, https://www.eset.com/afr/about/newsroom/press-releases-afr/research/dissection-of-sednit-espionage-group-1/, https://github.com/hfiref0x/UACME"
            ->  service.exploitationForPrivilegeEscalation
       
        | peripheralDeviceDiscovery
            ->  computer.peripheralDeviceDiscovery

        | portMonitors
            user info: "Adversaries can use this technique to load malicious code at startup that will persist on system reboot and execute as SYSTEM."
            ->  executeCode

        & emailStorageCollection
            user info: "Files containing email data can be acquired from a user's system, such as Outlook storage or cache files .pst and .ost."
            ->  dataCollected

        & outlookEmailCollection
            user info: "Some adversaries may acquire user credentials and access externally facing webmail applications, such as Outlook Web Access."
            ->  dataCollected
        
        & attemptApplicationShimming @hidden
            ->  applicationShimming
            
        | applicationShimming
            user info: "Utilizing these shims may allow an adversary to perform several malicious acts such as elevate privileges, install backdoors, disable defenses like Windows Defender, etc."
            ->  persistence, // use application shim databases for persistence
                hooking, // the shim database uses Hooking to redirect the code as necessary in order to communicate with the OS
                bypassUserAccountControl // certain shims can be used to Bypass User Account Control (UAC)

        & authenticationPackage
            user info: "Adversaries can use Windows Authentication Packages for persistence for persistence."
            ->  persistence   

        | changeDefaultFileAssociation
            ->  persistence

        | trustedDomainInfo // leads to SID-History Injection, Pass the Ticket, and Kerberoasting attacks
            user info: "The information discovered may help the adversary conduct SID-History Injection, Pass the Ticket, and Kerberoasting."

        & domainTrustDiscovery [Bernoulli(0.13)*Exponential(127.1)]
            developer info: "Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/domain-trust-discovery/, https://labs.sentinelone.com/trickbot-update-brief-analysis-of-a-recent-trickbot-payload/, https://www.wilbursecurity.com/2020/03/trickbot-to-ryuk-in-two-hours/, https://www.trustwave.com/en-us/resources/blogs/spiderlabs-blog/tale-of-the-two-payloads-trickbot-and-nitol/"
            ->  trustedDomainInfo

        & bootkit [Bernoulli(0.46)]
            developer info: "TTC assumed instant, see Dropbox/enterpriselang probability findings/bootkit.md for more info. sources used for distribution result: https://www.eset.com/afr/about/newsroom/press-releases-afr/research/dissection-of-sednit-espionage-group-1/, https://gs.statcounter.com/os-version-market-share/windows/desktop/worldwide, https://netmarketshare.com/operating-system-market-share.aspx?, https://www.fireeye.com/blog/threat-research/2015/12/fin1-targets-boot-record.html, https://www.hanselman.com/blog/HowManyPCsInTheWorldHaveTheNETFrameworkInstalled.aspx" 
            ->  persistence
            
        | videoCapture
            ->  computer.collectVideo

        | virtualizationOrSandboxEvasion [Bernoulli(0.18)]
            user info: "Adversaries may check for the presence of a virtual machine environment (VME) or sandbox to avoid potential detection of tools and activities."
            developer info: "Sources for distribution result: http://www.sharetech-me.com/wp-content/uploads/2015/06/21347932_GA-internet-security-threat-report-volume-20-2015-social_v2.pdf, https://docs.broadcom.com/docs/threats-to-virtual-environments-14-en"
            ->  bypassAntivirus,
                bypassHostForensicAnalysis,    
                bypassSignatureBasedDetection,  
                bypassStaticFileAnalysis              

        & xslScriptProcessing [Bernoulli(0.93)*Exponential(1)]
            developer info: "Sources used for probability distribution: https://reaqta.com/2018/03/spear-phishing-campaign-leveraging-msxsl/"
            user info: "Adversaries may abuse this functionality to execute arbitrary files while potentially bypassing application whitelisting defenses."
            ->  executeCode,
                windowsManagementInstrumentation, // leads to Windows Management Instrumentation attack
                bypassAntivirus,
                bypassApplicationWhitelisting,
                bypassDigitalCertificateValidation

        # activeDirectoryConfiguration
            user info: "https://attack.mitre.org/mitigations/M1015/"
            +>  passTheTicket

        # antivirus [Bernoulli(0.53)]
            user info: "https://attack.mitre.org/mitigations/M1049/"
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/5665797; https://www.sans.org/reading-room/whitepapers/analyst/endpoint-protection-response-survey-38460; https://www.businesswire.com/news/home/20100804005348/en/Cyveillance-Testing-Finds-AV-Vendors-Detect-Average"
            +>  softwarePacking,
                templateInjection

        # applicationIsolationAndSandboxing // Restrict execution of code to a virtual environment on or in transit to an endpoint system.
            user info: "https://attack.mitre.org/mitigations/M1048/"
            +>  _componentObjectModelAndDistributedCOM,
                dynamicDataExchange

        # audit 
            user info: "https://attack.mitre.org/mitigations/M1047/"
            +>  domainTrustDiscovery,
                bypassUserAccountControl,
                dLLSearchOrderHijacking,
                dLLSideLoading,
                fileSystemPermissionsWeakness,
                modifyExistingService,
                pathInterception,
                remoteDesktopProtocol,
                serverSoftwareComponent,
                scheduledTask

        # behaviorPreventionOnEndpoint
            user info : "https://attack.mitre.org/mitigations/M1040/"
            +>  dynamicDataExchange
        
        # codeSigning [Bernoulli(0.28)]
            user info: "https://attack.mitre.org/mitigations/M1045/"  
            +>  powerShell,
                _powerShellProfile,
                serverSoftwareComponent,
                lSASS_Driver         

        # credentialAccessProtection
            user info: "https://attack.mitre.org/mitigations/M1043/"
            ->  credentialDumping,
                lSASS_Driver

        # executionPrevention
            user info: "https://attack.mitre.org/mitigations/M1038/"
            +>  attemptAccessibilityFeatures,
                securitySoftwareDiscovery,
                appCertDLLs,
                appInitDLLs,
                cmstp,
                controlPanelItems,
                compiledHTMLFile,
                dLLSearchOrderHijacking,
                executionThroughAPI,
                executionThroughModuleLoad,
                installUtil,
                mshta,
                networkShareDiscovery,
                pathInterception,
                queryRegistery,
                regsvcsOrRegasm, 
                screensaver,
                signedBinaryProxyExecution,
                signedScriptProxyExecution,
                sIPAndTrustProviderHijacking,
                systemTimeDiscovery,
                trustedDeveloperUtilities,
                winlogonHelperDLL,
                xslScriptProcessing,
                computer.router.internalNetwork.networkSharedDrive.taintSharedContent

        # exploitProtection
            user info: "https://attack.mitre.org/mitigations/M1050/"
            +>  regsvr32,
                rundll32,
                computer.router.internalNetwork.networkSharedDrive.taintSharedContent

        # limitHardwareInstallation
            user info: "https://attack.mitre.org/mitigations/M1034/"
            developer info: "Binary probability relation."
            +>  replicationThroughRemovableMedia

        # operatingSystemConfiguration
            user info: "https://attack.mitre.org/mitigations/M1028/" 
            +>  passwordFilterDLL,
                bITSJobs,
                remoteDesktopProtocol,
                scheduledTask   

        # passwordPolicies 
            user info: "https://attack.mitre.org/mitigations/M1027/" 
            +>  passTheHash,
                passTheTicket,
                windowsAdminShares               

        # privilegedProcessIntegrity  
            user info: "https://attack.mitre.org/mitigations/M1025/"
            ->  authenticationPackage,
                credentialDumping,
                securitySupportProvider 

        # privilegedAccountManagement [Bernoulli(0.30)]
            developer info: "Sources for distribution results : https://lp.alpha-gen.co.uk/2018-global-state-of-pam/"
            user info: "https://attack.mitre.org/mitigations/M1026/"
            +>  bootkit,
                bypassUserAccountControl,
                componentObjectModelAndDistributedCOM,
                passTheHash,
                scheduledTask

        # restrictFileAndDirectoryPermissions
            user info: "https://attack.mitre.org/mitigations/M1022/"
            +>  controlPanelItems,
                dLLSideLoading,
                indicatorBlocking,
                logonScripts,
                modifyRegistry,
                ntfsFileAttributes,
                pathInterception,
                sharedWebroot,
                sIPAndTrustProviderHijacking,
                serviceStop,
                serviceExecution,
                _serviceRegistryPermissionsWeakness,
                timeProviders,
                computer.router.internalNetwork.networkSharedDrive.taintSharedContent  

        # restrictRegistryPermissions // Restrict the ability to modify certain hives or keys in the Windows Registry.
            user info: "https://attack.mitre.org/mitigations/M1024/"
            ->  sIPAndTrustProviderHijacking,
                _powerShellProfile,
                serviceStop,
                timeProviders

        # restrictLibraryLoading 
            user info: "https://attack.mitre.org/mitigations/M1044/"
            ->  dLLSearchOrderHijacking,
                lSASS_Driver       

        # softwareConfiguration
            user info: "https://attack.mitre.org/mitigations/M1054/"
            +>  indicatorBlocking,
                officeApplicationStartup,
                _powerShellProfile,
                dynamicDataExchange

        # bootIntegrity 
            user info: "https://attack.mitre.org/mitigations/M1046/"
            +>  bootkit,
                systemFirmware      

        # encryptSensitiveInformation
            user info: "https://attack.mitre.org/mitigations/M1041/"
            +>  emailStorageCollection,
                outlookEmailCollection

        # disableOrRemoveFeatureOrProgram
            user info: "https://attack.mitre.org/mitigations/M1042/"
            +>  cmstp,
                componentObjectModelAndDistributedCOM,
                installUtil,
                lLMNR_NBT_NS_PoisoningAndRelay,
                mshta,
                officeApplicationStartup,
                powerShell,
                registryKeysEnabled,
                regsvcsOrRegasm,
                remoteDesktopProtocol,
                replicationThroughRemovableMedia,
                screensaver,
                templateInjection,
                trustedDeveloperUtilities,
                windowsRemoteManagement,
                dynamicDataExchange

        # filterNetworkTraffic // Use host-based security software to block LLMNR/NetBIOS traffic.
            user info: "https://attack.mitre.org/mitigations/M1037/"   
            ->  lLMNR_NBT_NS_PoisoningAndRelay,
                forcedAuthentication 

        # updateSoftware [Bernoulli(0.34)]
            user info: "https://attack.mitre.org/mitigations/M1051/" 
            +>  appInitDLLs,
                attemptApplicationShimming,
                officeApplicationStartup,
                passTheHash
        
        # userAccountControl
            user info: "https://attack.mitre.org/mitigations/M1052/"
            +>  attemptApplicationShimming, 
                bypassUserAccountControl,
                fileSystemPermissionsWeakness,
                passTheHash

    }

    asset Linux extends OS {
        & bootkit [Bernoulli(0.46)]
            developer info: "TTC assumed instant, see Dropbox/enterpriselang probability findings/bootkit.md for more info. sources used for distribution result: https://www.eset.com/afr/about/newsroom/press-releases-afr/research/dissection-of-sednit-espionage-group-1/, https://gs.statcounter.com/os-version-market-share/windows/desktop/worldwide, https://netmarketshare.com/operating-system-market-share.aspx?, https://www.fireeye.com/blog/threat-research/2015/12/fin1-targets-boot-record.html, https://www.hanselman.com/blog/HowManyPCsInTheWorldHaveTheNETFrameworkInstalled.aspx"
            ->  persistence

        & _clearCommandHistory @hidden
            ->  clearCommandHistory       

        & clearCommandHistory
            user info: "Adversaries can abuse this by searching these files for cleartext passwords. Additionally, adversaries can use a variety of methods to prevent their own commands from appear in these logs." 
            ->  bypassLogAnalysis, 
                bypassHostForensicAnalysis

        | spaceAfterFileName [Exponential(1)]
            developer info: "Binary probability distribution. Based on the abuse of system features. Sources: https://attack.mitre.org/techniques/T1151/, https://www.welivesecurity.com/2016/07/06/new-osxkeydnap-malware-hungry-credentials/"
            ->  computer.user.attemptUserExecution

        | infectedOS
            +>  bashHistory

        | _bash_profileAndBashrc @hidden
            ->  bash_profileAndBashrc

        & bash_profileAndBashrc
            user info: "Adversaries can insert code into these files to gain persistence each time a user logs in or opens a new shell."
            ->  persistence

        & bashHistory
            ->  indicatorRemovalOnHost,
                adminAccount.windowsAdmin.adminCredentials,
                userAccount.userCredentials

        & hISTCONTROL
            user info: "Adversaries can use this to operate without leaving traces by simply prepending a space to all of their terminal commands."     
            ->  bypassLogAnalysis,
                bypassHostForensicAnalysis

        & kernelModulesAndExtensions
            user info: "Adversaries can use loadable kernel modules to covertly persist on a system and evade defenses."
            ->  rootkit        

        & localJobScheduling
            user info: "An adversary may use job scheduling to execute programs at system startup or on a scheduled basis for Persistence, to conduct Execution as part of Lateral Movement, to gain root privileges, or to run a process under the context of a specific account."
            ->  persistence,
                adminAccount.rootAdmin.adminRights

        & portKnocking
            user info: "This technique has been observed to both for the dynamic opening of a listening port as well as the initiating of a connection to a listening server on a different system."
            ->  bypassDefensiveNetworkServiceScanning  

        & serverSoftwareComponent
            user info: "Adversaries may abuse legitimate extensible development features of server applications to establish persistent access to systems."
            ->  persistence

        & setuidAndSetgid
            user info: "An adversary can take advantage of this to either do a shell escape or exploit a vulnerability in an application with the setsuid or setgid bits to get code running in a different user's context."
            ->  service.exploitationForPrivilegeEscalation,
                persistence  

        | source
            developer info: "Binary probability distribution."
            user info:"The source command loads functions into the current shell or executes files in the current context."
            ->  executeCode

        & sshHijacking
            user info: "In order to move laterally from a compromised host, adversaries may take advantage of trust relationships established with other systems via public key authentication in active SSH sessions by hijacking an existing connection to another system."
            ->  sSHCredentialInterception

        | _sudo @hidden
            ->  sudo

        & sudo
            user info: "Adversaries can take advantage of these configurations to execute commands as other users or spawn processes with higher privileges."
            ->  service.exploitationForPrivilegeEscalation

        | _sudoCaching @hidden
            ->  sudoCaching

        & sudoCaching
            user info: "Adversaries can abuse poor configurations of this to escalate privileges without needing the user's password."
            ->  service.exploitationForPrivilegeEscalation 

        | _systemdService @hidden
            ->  systemdService

        & systemdService
            user info: "Adversaries have used systemd functionality to establish persistent access to victim systems by creating and/or modifying service unit files that cause systemd to execute malicious commands at recurring intervals, such as at system boot."
            ->  persistence

        | timestomp
            user info: "Timestomp is done on files that have been modified or created by the adversary so that they do not appear conspicuous to forensic investigators or file analysis tools."
            ->  bypassHostForensicAnalysis

        | trap [Exponential(0.1)]
            user info: "Adversaries can use this to register code to be executed when the shell encounters specific interrupts either to gain execution or as a persistence mechanism."
            developer info: "Arbitrary distribution. See trap.md file."
            ->  executeCode,
                persistence

        | executeCode 
            +>  computer.infectedLinuxComputer 

        # antivirus [Bernoulli(0.53)]
            user info: "https://attack.mitre.org/mitigations/M1049/"
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/5665797; https://www.sans.org/reading-room/whitepapers/analyst/endpoint-protection-response-survey-38460; https://www.businesswire.com/news/home/20100804005348/en/Cyveillance-Testing-Finds-AV-Vendors-Detect-Average"
            +>  kernelModulesAndExtensions

        # audit 
            user info: "https://attack.mitre.org/mitigations/M1047/"
            +>  serverSoftwareComponent

        # bootIntegrity 
            user info: "https://attack.mitre.org/mitigations/M1046/"
            +>  bootkit

        # codeSigning [Bernoulli(0.28)]
            user info: "https://attack.mitre.org/mitigations/M1045/"  
            +>  serverSoftwareComponent

        # disableOrRemoveFeatureOrProgram
            user info: "https://attack.mitre.org/mitigations/M1042/"
            +>  sshHijacking

        # executionPrevention
            +>  kernelModulesAndExtensions

        # environmentVariablePermissions // Prevent modification of environment variables by unauthorized users and groups.
            user info: "https://attack.mitre.org/mitigations/M1039/"
            ->  clearCommandHistory,
                hISTCONTROL              

        # limitSoftwareInstallation
            user info: "https://attack.mitre.org/mitigations/M1033/"
            +>  systemdService

        # operatingSystemConfiguration
            user info: "https://attack.mitre.org/mitigations/M1028/" 
            +>  bashHistory,
                hISTCONTROL,
                setuidAndSetgid,
                sudoCaching

        # passwordPolicies // Set and enforce secure password policies for accounts 
            user info: "https://attack.mitre.org/mitigations/M1027/"
            +>  sshHijacking

        # privilegedAccountManagement [Bernoulli(0.30)]
            developer info: "Sources for distribution results : https://lp.alpha-gen.co.uk/2018-global-state-of-pam/"
            user info: "https://attack.mitre.org/mitigations/M1026/"
            +>  bootkit,
                sshHijacking   

        # restrictFileAndDirectoryPermissions
            user info: "https://attack.mitre.org/mitigations/M1022/"
            +>  bash_profileAndBashrc,
                sshHijacking,
                sudo,
                systemdService   

        # userAccountControl
            user info: "https://attack.mitre.org/mitigations/M1052/"
            +>  sshHijacking    
    }

    asset MacOS extends OS {
        & bashHistory
            ->  indicatorRemovalOnHost,
                adminAccount.rootAdmin.adminCredentials,
                userAccount.userCredentials

        | _bash_profileAndBashrc @hidden
            ->  bash_profileAndBashrc

        & bash_profileAndBashrc
            user info: "Adversaries can insert code into these files to gain persistence each time a user logs in or opens a new shell."
            ->  persistence

        | codeSigningCertificate [Bernoulli(0.72)*Exponential(96)]
            user info: "Adversaries are known to use code signing certificates to masquerade malware and tools as legitimate binaries. Code signing certificates may be used to bypass security policies that require signed code to execute on a system."
            developer info: "Sources for distribution results:https://www.venafi.com/blog/study-how-well-are-you-protecting-code-signing-certificates#:~:text=The%20study%20found%20that%20although,process%20for%20code%20signing%20certificates,https://duo.com/decipher/attackers-are-signing-malware-with-valid-certificates,https://cqureacademy.com/blog/windows-internals/code-signing"

        & _clearCommandHistory @hidden
            ->  clearCommandHistory       

        & clearCommandHistory
            user info: "Adversaries can abuse this by searching these files for cleartext passwords. Additionally, adversaries can use a variety of methods to prevent their own commands from appear in these logs." 
            ->  bypassLogAnalysis, 
                bypassHostForensicAnalysis

        | _dylibHijacking @hidden   
            ->  dylibHijacking 

        & dylibHijacking [Bernoulli(0.16)] 
            user info: "Adversaries can take advantage of ambiguous paths to plant dylibs to gain privilege escalation or persistence."
            developer info: "Based on the results for dLLSearchOrderHijacking.  Sources used for probability distribution: https://redcanary.com/threat-detection-report/techniques/dll-search-order-hijacking/. Sources for TTC (not used): http://blog.nsfocus.net/dridex-banking-malware-sample-technical-analysis-solution/, https://threatresearch.ext.hp.com/dridex-threat-analysis-july-2019-variant/"
            ->  service.exploitationForPrivilegeEscalation // leads to Exploitation for Privilege Escalation attack

        & elevatedExecutionWithPrompt
            user info: "Adversaries may leverage the AuthorizationExecuteWithPrivileges API to escalate privileges by prompting the user for credentials."
            ->  service.exploitationForPrivilegeEscalation,
                masquerading // combined with Masquerading to trick the user into granting escalated privileges to malicious code

        & emond
            user info: "Adversaries may use Event Monitor Daemon (emond) to establish persistence by scheduling malicious commands to run on predictable event triggers."
            ->  persistence,
                service.exploitationForPrivilegeEscalation

        | executeCode 
            +>  computer.infectedMacOSComputer   

        & gatekeeperBypass
            user info: "Apps loaded onto the system from USB flash drive, optical disk, external hard drive, or even from a drive shared over the local network won't set this flag. Additionally, other utilities or events like drive-by downloads don't necessarily set it either. This completely bypasses the built-in Gatekeeper check."
            ->  bypassApplicationWhitelisting,
                bypassAntivirus 

        & hiddenUsers
            user info: "By using the Create Account technique with a userID under 500 and enabling this property (setting it to Yes), an adversary can hide their user accounts much more easily."
            ->  adminAccount.createUserAccount

        & hiddenWindow [Bernoulli(0.22)]
            user info: "A common use for this is when applications run in the system tray, but don't also want to show up in the Dock. However, adversaries can abuse this feature and hide their running window."
            developer info: "Sources for probability distribution: https://www.carbonblack.com/resources/threat-research/cybersecurity-outlook-report/#download"

        & hISTCONTROL
            user info: "Adversaries can use this to operate without leaving traces by simply prepending a space to all of their terminal commands."     
            ->  bypassLogAnalysis,
                bypassHostForensicAnalysis

        | infectedOS
            +>  bashHistory

        & inputPrompt
            user info: "Adversaries may mimic this functionality to prompt users for credentials with a seemingly legitimate prompt for a number of reasons that mimic normal usage."
            ->  userAccount.userCredentials

        & kernelModulesAndExtensions
            user info: "Adversaries can use loadable kernel modules to covertly persist on a system and evade defenses."
            ->  rootkit  

        & launchAgent
            user info: "Adversaries may install a new launch agent that can be configured to execute at login by using launchd or launchctl to load a plist into the appropriate directories."     
            ->  persistence

        & launchctl
            developer info: "Binary probability distribution."
            user info: "Adversaries can abuse this functionality to execute code or even bypass whitelisting if launchctl is an allowed process."
            ->  executeCode,
                bypassApplicationWhitelisting,
                bypassProcessWhitelisting,
                bypassFileOrPathWhitelisting  

        & launchDaemon
            user info: "Launch Daemons may be created with administrator privileges, but are executed under root privileges, so an adversary may also use a service to escalate privileges from administrator to root. Adversary can modify a current Launch Daemon's executable and gain persistence or Privilege Escalation."
            ->  persistence,
                service.exploitationForPrivilegeEscalation         

        & lC_LOAD_DYLIB_Addition
            user info: "Adversaries can remediate the issue that any changes will invalidate digital signatures on binaries because the binary is being modified by simply removing the LC_CODE_SIGNATURE command from the binary so that the signature isn't checked at load time."
            ->  persistence

        & lC_MAINHijacking
            user info: "LC_MAIN that points to the binary's entry point for execution and can be hijacked so that initial execution flows to a malicious addition. By modifying a binary in this way, application whitelisting can be bypassed because the file name or application path is still the same."
            ->  bypassApplicationWhitelisting,
                bypassProcessWhitelisting,
                bypassFileOrPathWhitelisting 

        & localJobScheduling
            developer info: "Binary probability distribution."
            user info: "An adversary may use job scheduling to execute programs at system startup or on a scheduled basis for Persistence,  to conduct Execution as part of Lateral Movement, to gain root privileges, or to run a process under the context of a specific account."
            ->  persistence,
                adminAccount.rootAdmin.adminRights

        & loginItem
            user info: "If an adversary can register their own login item or modified an existing one, then they can use it to execute their code for a persistence mechanism each time the user logs in."
            ->  persistence      

        & logonScripts
            ->  adminAccount.rootAdmin.adminRights  

        & plistModification
            user info: "Adversaries can modify these plist files to point to their own code, can use them to execute their code in the context of another user, bypass whitelisting procedures, or even use them as a persistence mechanism."
            ->  bypassApplicationWhitelisting,
                bypassProcessWhitelisting,
                bypassFileOrPathWhitelisting,
                executeCode,
                persistence

        & portKnocking
            user info: "This technique has been observed to both for the dynamic opening of a listening port as well as the initiating of a connection to a listening server on a different system."
            ->  bypassDefensiveNetworkServiceScanning

        & rc_common
            user info: "Adversaries can use the rc.common file as a way to hide code for persistence that will execute on each reboot as the root user."
            ->  persistence

        & reopenedApplications
            user info: "An adversary can modify one of these files directly to include a link to their malicious executable to provide a persistence mechanism each time the user reboots their machine."
            ->  persistence

        | securitydMemory
            user info: "If an adversary can obtain root access (allowing them to read securityd's memory), then they can scan through memory to find the correct sequence of keys in relatively few tries to decrypt the user's logon keychain."
            ->  keychainCollection

        & setuidAndSetgid
            user info: "An adversary can take advantage of this to either do a shell escape or exploit a vulnerability in an application with the setsuid or setgid bits to get code running in a different user's context."
            ->  service.exploitationForPrivilegeEscalation,
                persistence

        | source
            developer info: "Binary probability distribution."
            ->  executeCode

        & sshHijacking
            user info: "In order to move laterally from a compromised host, adversaries may take advantage of trust relationships established with other systems via public key authentication in active SSH sessions by hijacking an existing connection to another system."
            ->  sSHCredentialInterception

        | _sudo @hidden
            ->  sudo
            
        & sudo
            user info: "Adversaries can take advantage of these configurations to execute commands as other users or spawn processes with higher privileges."
            ->  service.exploitationForPrivilegeEscalation  

        | _sudoCaching @hidden
            ->  sudoCaching
            
        & sudoCaching
            user info: "Adversaries can abuse poor configurations of this to escalate privileges without needing the user's password."
            ->  service.exploitationForPrivilegeEscalation

        | trap [Exponential(0.1)]
            developer info: "Arbitrary distribution. See trap.md file."
            user info: "Adversaries can use this to register code to be executed when the shell encounters specific interrupts either to gain execution or as a persistence mechanism."
            ->  executeCode,
                persistence

        | videoCapture
            ->  computer.collectVideo     

        | spaceAfterFileName [Exponential(1)]
            developer info: "Binary probability distribution. Based on the abuse of system features. Sources: https://attack.mitre.org/techniques/T1151/, https://www.welivesecurity.com/2016/07/06/new-osxkeydnap-malware-hungry-credentials/"
            ->  computer.user.attemptUserExecution

        & networkShareDiscovery
            ->  computer.router.internalNetwork.networkShareDiscovery

        & securitySoftwareDiscovery [Bernoulli(0.83387)]
            user info: "Adversaries may attempt to get a listing of security software, configurations, defensive tools, and sensors that are installed on the system. This may include things such as local firewall rules and anti-virus. These checks may be built into early-stage remote access tools."
            developer info: "TTC Assumed instant. Sources for probability distribution result: https://krisk.io/post/sok-attack-securecomm19.pdf, https://www.recordedfuture.com/mitre-attack-tactics/, https://attack.mitre.org/techniques/T1063/"
            ->  service._exploitationForDefenseEvasion // leads to exploitation for defense evasion attack

        | windowsManagementInstrumentation
            ->  scripting  

        & keychainCollection
            user info: "If an adversary knows the credentials for the login keychain, then they can get access to all the other credentials stored in this vault."
            ->  userAccount.userCredentials

        & appleScript
            developer info: "Binary probability distribution."
            ->  executeCode,
                inputPrompt // leads to Input Prompt attack
                
        & _startupItems @hidden
            ->  startupItems

        & startupItems
            user info: "An adversary can create the appropriate folders/files in the StartupItems directory to register their own persistence mechanism."
            ->  persistence,
                service.exploitationForPrivilegeEscalation // leads to Exploitation for Privilege Escalation attack 

        # antivirus [Bernoulli(0.53)]
            user info: "https://attack.mitre.org/mitigations/M1049/"
            developer info: "Sources for distribution result: https://ieeexplore.ieee.org/abstract/document/5665797; https://www.sans.org/reading-room/whitepapers/analyst/endpoint-protection-response-survey-38460; https://www.businesswire.com/news/home/20100804005348/en/Cyveillance-Testing-Finds-AV-Vendors-Detect-Average"
            +>  kernelModulesAndExtensions

        # audit 
            user info: "https://attack.mitre.org/mitigations/M1047/"
            +>  lC_LOAD_DYLIB_Addition

        # codeSigning [Bernoulli(0.28)]
            user info: "https://attack.mitre.org/mitigations/M1045/"  
            +>  appleScript,
                lC_LOAD_DYLIB_Addition,
                lC_MAINHijacking

        # disableOrRemoveFeatureOrProgram
            user info: "https://attack.mitre.org/mitigations/M1042/"
            +>  emond,
                reopenedApplications,
                sshHijacking

        # executionPrevention
            +>  securitySoftwareDiscovery,
                elevatedExecutionWithPrompt,
                gatekeeperBypass,
                hiddenWindow,
                kernelModulesAndExtensions,
                lC_LOAD_DYLIB_Addition,
                networkShareDiscovery         

        # environmentVariablePermissions // Prevent modification of environment variables by unauthorized users and groups.
            user info: "https://attack.mitre.org/mitigations/M1039/"
            ->  clearCommandHistory,
                hISTCONTROL   

        # operatingSystemConfiguration
            user info: "https://attack.mitre.org/mitigations/M1028/" 
            +>  bashHistory,
                hiddenUsers,
                hISTCONTROL,
                setuidAndSetgid,
                sudoCaching

        # passwordPolicies // Set and enforce secure password policies for accounts 
            user info: "https://attack.mitre.org/mitigations/M1027/"
            +>  keychainCollection,
                sshHijacking

        # privilegedAccountManagement [Bernoulli(0.30)]
            developer info: "Sources for distribution results : https://lp.alpha-gen.co.uk/2018-global-state-of-pam/"
            user info: "https://attack.mitre.org/mitigations/M1026/"
            +>  sshHijacking

        # restrictFileAndDirectoryPermissions
            user info: "https://attack.mitre.org/mitigations/M1022/"
            +>  bash_profileAndBashrc,
                dylibHijacking,
                logonScripts,
                plistModification,
                sshHijacking,
                startupItems,
                sudo

        # userAccountControl
            user info: "https://attack.mitre.org/mitigations/M1052/"
            +>  sshHijacking
    }    
}

category Network {
    asset Router {
        
    }

    asset InternalNetwork {
        | obfuscateNetworkTraffic

        # sSLOrTLSInspection
            user info: "https://attack.mitre.org/mitigations/M1020/"
            ->  obfuscateNetworkTraffic,
                router.computer.os.standardCryptographicProtocol

        & applicationLayerConnexion
            ->  c2Connexion,
                bypassNetworkIntrusionDetection,
                router.externalNetwork.bypassNetworkIntrusionDetection

        & dataExfiltration
            ->  router.externalNetwork.dataExfiltration

        | attemptDefacement @hidden
            ->  defacement

        & defacement {I} [Bernoulli(0.31)*Exponential(10.67)]
            user info: "An adversary may deface systems internal to an organization in an attempt to intimidate or mislead users."
            developer info: "Time here is: assumed time defacement will last. Sources used for probability distribution result: https://www.google.com/url?sa=t&rct=j&q=&esrc=s&source=web&cd=&ved=2ahUKEwi5m_fWpY7qAhUaxMQBHU6yBxoQFjAKegQIMhAB&url=https%3A%2F%2Fdocuments.trendmicro.com%2Fassets%2Fwhite_papers%2Fwp-a-deep-dive-into-defacement.pdf&usg=AOvVaw2zOWHBoWsMXdfxK_fLTP_G, https://www.internetlivestats.com/total-number-of-websites/, https://www.helpnetsecurity.com/2020/04/03/back-up-data/, https://books.google.se/books?id=c7CpBAAAQBAJ&printsec=frontcover&dq=Cyber+Blockades&hl=en&sa=X&ved=0ahUKEwj4qsnGq47qAhVRXpoKHWACBqkQ6AEIKDAA#v=onepage&q=Cyber%20Blockades&f=false"
        
        E firewallExists
            <-  router.firewall
            ->  router.firewall._bypassFirewall
               
        | bypassAccessControl

        | bypassFirewall

        | bypassNetworkIntrusionDetection 

        | bypassProxies

        & transmittedDataManipulation {I}
            user info: "By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making. "

        & c2Connexion
            ->  c2Connected

        & exchangeServerCollection
            user info: "Adversaries may leverage a user's credentials and interact directly with the Exchange server to acquire information from within a network."
            ->  router.computer.os.dataCollected

        | networkShareDiscovery
            user info: "Networks often contain shared network drives and folders that enable users to access file directories on various systems across a network. "
        
        | internalNetworkResourcesInformation

        | remoteSystemsConnection
            ->  router.computer.router.internalNetwork.networkSharedDrive.dataFromNetworkSharedDrive 

        | webShell
            user info: "Web shells may serve as Redundant Access or as a persistence mechanism."
            ->  router.computer.os.redundantAccess, // use a Web Shell to maintain access 
                persistence

        | persistence 
        
        | remoteAccess

        | remoteCOMExecution
            user info: "Adversaries may use the Windows Component Object Model (COM) and Distributed Component Object Model (DCOM) to execute on remote systems as part of lateral movement."

        | remoteExecution

        | c2Connected 
            ->  communicate,
                dataObfuscation,
                packetCapture

        | communicate 

        | generateDomainNames 

        | _dataObfuscation @hidden
            ->  dataObfuscation

        & dataObfuscation [Bernoulli(0.685)]
            user info: "This encompasses many methods, such as adding junk data to protocol traffic, using steganography, commingling legitimate traffic with C2 communications traffic, or using a non-standard data encoding system."
            developer info: "TTC instant/not applicable. sources used for probability distribution: https://www.academia.edu/7383312/Obfuscation_The_Hidden_Malware, https://books.google.se/books?id=tHK6CAAAQBAJ&pg=PA175&lpg=PA175&dq=percent+of+malware+using+obfuscation&source=bl&ots=uAffxqpZX2&sig=ACfU3U2kkIrqzLLQ8ipOR2RwkRPzc8Jp1A&hl=en&sa=X&ved=2ahUKEwio69vRhZrqAhVMEncKHYwLAwsQ6AEwC3oECGQQAQ#v=onepage&q=percent%20of%20malware%20using%20obfuscation&f=false"
            ->  router.externalNetwork.bypassNetworkIntrusionDetection,
                bypassNetworkIntrusionDetection

        | packetCapture

        # encryptSensitiveInformation
            user info:"https://attack.mitre.org/mitigations/M1041/"   
            ->  transmittedDataManipulation,
                exchangeServerCollection

        # executionPrevention             
            user info: "https://attack.mitre.org/mitigations/M1038/"
            ->  router.computer.os.networkServiceScan

        # filterNetworkTraffic
            user info: "https://attack.mitre.org/mitigations/M1037/"   
            ->  router.computer.os.customCommandAndControlProtocol,
                router.computer.os.endpointDenialOfService,
                router.computer.os.multiHopProxy,
                router.computer.os.networkDenialOfService,
                router.computer.os.standardNonApplicationLayerProtocol,
                router.computer.os.windows.bITSJobs 

        # limitAccessToResourceOverNetwork
            user info: "https://attack.mitre.org/mitigations/M1035/"   
            ->  router.computer.os.windows.remoteDesktopProtocol,
                router.computer.os.windows.sharedWebroot,
                router.computer.hardwareAddition.hardwareAdditions,
                router.computer.os.service.externalRemoteServices

        # multiFactorAuthentication
            user info: "https://attack.mitre.org/mitigations/M1032/"
            ->  exchangeServerCollection,
                router.computer.os.service.externalRemoteServices,
                router.externalNetwork.dataFromCloudStorageObject

        # networkIntrusionPrevention
            user info: "https://attack.mitre.org/mitigations/M1031/"
            ->  applicationLayerConnexion,
                c2Connexion,
                dataExfiltration,
                generateDomainNames,
                router.computer.os.service.browser.spearphishingAttachment,
                router.computer.os.commonlyUsedPortsConnection,
                router.computer.os.connectionProxy,
                router.computer.os.customCommandAndControlProtocol,
                router.computer.os.customCryptographicProtocol,
                router.computer.os.dataCompressed,
                router.computer.os.dataEncoding,
                router.computer.os.fallbackChannels,
                router.computer.os.multibandCommunication,
                router.computer.os.multilayerEncryption,
                router.computer.os.multiStageChannels,
                router.computer.os.networkServiceScan,
                router.computer.os.redundantAccess,
                router.computer.os.remoteFileCopy,
                router.computer.os.standardCryptographicProtocol,
                router.computer.os.standardApplicationLayerProtocol,
                router.computer.os.standardNonApplicationLayerProtocol,
                router.computer.os.uncommonlyUsedPortsConnection,
                router.computer.os.windows.templateInjection

        # networkSegmentation
            user info: "https://attack.mitre.org/mitigations/M1030/"
            ->  remoteAccess,
                router.computer.os.service.applicationDeploymentSoftware,
                router.computer.os.adminAccount.createUserAccount,
                router.computer.os.commonlyUsedPortsConnection,
                router.computer.os.uncommonlyUsedPortsConnection,
                router.computer.os.customCommandAndControlProtocol,
                router.computer.os.privateKeys,
                router.computer.os.runtimeDataManipulation,
                router.computer.os.standardNonApplicationLayerProtocol,
                router.computer.os.windows.accountManipulation,
                router.computer.os.windows.componentObjectModelAndDistributedCOM,
                router.computer.os.windows.domainTrustDiscovery,
                router.computer.os.windows.serviceStop,
                router.computer.os.windows.sharedWebroot,
                router.computer.os.service.exploitationOfRemoteServices,
                router.computer.os.service.exploitHighVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitMediumVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitLowVulnerabilityPublicFacingApplication,
                router.computer.os.service.externalRemoteServices,
                router.computer.os.service.thirdpartySoftware.trustedRelationship,
                router.computer.os.service.thirdpartySoftware._useThirdpartySoftware

        # vulnerabilityScanning [Bernoulli(0.41)] // Regularly scan the internal network for available services to identify new and potentially vulnerable services.
            user info: "https://attack.mitre.org/mitigations/M1016/"
            developer info: "Sources for distribution result : https://static.tenable.com/marketing/research-reports/Research-Report-Ponemon-Institute-Measuring_and_Managing_the_Cyber_Risks_to_Business_Operations.pdf, https://www.computerweekly.com/news/252462476/Most-businesses-vulnerable-to-supply-chain-cyber-attacks#:~:text=Seven%20in%2010%20businesses%20may,to%20research%20commissioned%20by%20Accenture"
            ->  router.computer.os.service.exploitHighVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitMediumVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitLowVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitationOfRemoteServices,
                router.computer.os.service.supplyChainCompromise
    }

    asset NetworkSharedDrive extends InternalNetwork {
        & taintSharedContent
            user info: "Once a user opens the shared tainted content, tfhe malicious portion can be executed to run the adversary's code on a remote system."
            ->  router.computer.os.windows.executeCode

        | dataFromNetworkSharedDrive
            user info: "Adversaries may search network shares on computers they have compromised to find files of interest."
            ->  router.computer.os.sensitiveDataCollected
    }

    asset ExternalNetwork {
        | obfuscateNetworkTraffic

        | bypassFirewall

        | bypassNetworkIntrusionDetection

        | dataExfiltration

        & dataFromCloudStorageObject
            user info: "Adversaries may access data objects from improperly secured cloud storage."

        | attemptDefacement @hidden
            ->  defacement

        & defacement {I}
            user info: "Websites are a common victim of defacement that often targeted by adversary and hacktivist groups in order to push a political message or spread propaganda."
            ->  router.computer.os.service.browser._websiteDefacement

        & webService
            user info: "Adversaries may use an existing, legitimate external Web service as a means for relaying commands to a compromised system."
            ->  router.computer.os.bypassBinaryAnalysis,
                router.computer.os.bypassLogAnalysis,
                bypassFirewall,
                router.internalNetwork.bypassFirewall,
                router.internalNetwork.c2Connected

        | attemptTransmittedDataManipulation @hidden {I}
            ->  transmittedDataManipulation
 
        & transmittedDataManipulation {I} [Bernoulli(0.65833)*Exponential(24)]
            user info: "By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making."
            developer info: "Sources used for distribution: https://www.google.com/url?sa=t&rct=j&q=&esrc=s&source=web&cd=&ved=2ahUKEwiW28Dmk4vqAhXS4KYKHZvADy4QFjABegQIAhAB&url=https%3A%2F%2Fgo.thalesesecurity.com%2Frs%2F480-LWA-970%2Fimages%2F2019-DTR-Global-USL-Web.pdf&usg=AOvVaw2rKeGVWQgkIV7M0vjnCo1g, https://secure2.sophos.com/en-us/medialibrary/Gated%20Assets/white%20papers/the-state-of-encryption-today-wpna.pdf?cmp=701j0000001WskZAAS, www.businesswire.com/news/home/20190221005061/en/Survey-83-Percent-U.S.-Organizations-Accidentally-Exposed, https://content.fireeye.com/apt/rpt-apt38"
            
        # encryptSensitiveInformation
            user info:"https://attack.mitre.org/mitigations/M1041/"   
            ->  transmittedDataManipulation

        # filterNetworkTraffic // Modify network and/or host firewall rules, as well as other network controls, to only allow legitimate BITS traffic.
            user info: "https://attack.mitre.org/mitigations/M1037/"   
            ->  dataFromCloudStorageObject,
                router.computer.os.endpointDenialOfService,
                router.computer.os.multiHopProxy,
                router.computer.os.networkDenialOfService,
                router.computer.os.standardNonApplicationLayerProtocol,
                router.computer.os.windows.bITSJobs  

        # networkIntrusionPrevention
            user info: "https://attack.mitre.org/mitigations/M1031/"
            ->  webService,
                router.computer.os.service.browser.spearphishingAttachment,
                router.internalNetwork.dataObfuscation,
                router.internalNetwork.generateDomainNames,
                router.computer.os.commonlyUsedPortsConnection,
                router.computer.os.uncommonlyUsedPortsConnection,
                router.computer.os.connectionProxy,
                router.computer.os.dataEncoding,
                router.computer.os.fallbackChannels,
                router.computer.os.multibandCommunication,
                router.computer.os.multilayerEncryption,
                router.computer.os.multiStageChannels,
                router.computer.os.redundantAccess,
                router.computer.os.remoteFileCopy,
                router.computer.os.standardCryptographicProtocol,
                router.computer.os.standardApplicationLayerProtocol,
                router.computer.os.standardNonApplicationLayerProtocol

        # networkSegmentation // Segment networks and systems appropriately to reduce access to critical systems and services to controlled methods.
            user info: "https://attack.mitre.org/mitigations/M1030/"
            ->  router.computer.os.service.exploitHighVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitMediumVulnerabilityPublicFacingApplication,
                router.computer.os.service.exploitLowVulnerabilityPublicFacingApplication,
                router.computer.os.service.applicationDeploymentSoftware,
                router.computer.os.service.exploitationOfRemoteServices,
                router.computer.os.commonlyUsedPortsConnection,
                router.computer.os.uncommonlyUsedPortsConnection,
                router.computer.os.privateKeys,
                router.computer.os.runtimeDataManipulation,
                router.computer.os.standardNonApplicationLayerProtocol,
                router.computer.os.windows.accountManipulation,
                router.computer.os.windows.serviceStop         

        # sSLOrTLSInspection
            user info: "https://attack.mitre.org/mitigations/M1020/"
            ->  obfuscateNetworkTraffic,
                router.computer.os.standardCryptographicProtocol
    }

    asset Firewall {
        & _bypassFirewall @hidden
            ->  bypassFirewall,
                router.internalNetwork.dataExfiltration,
                router.internalNetwork.c2Connexion

        | bypassFirewall

        # filterNetworkTraffic // Modify network and/or host firewall rules, as well as other network controls, to only allow legitimate BITS traffic.
            user info: "https://attack.mitre.org/mitigations/M1037/"   
            ->  router.computer.os.service.cloudInstanceMetadataAPI,
                router.computer.os.windows.bITSJobs,
                router.computer.os.linux.portKnocking,
                router.computer.os.macOS.portKnocking

        # networkSegmentation // Properly configure firewalls and proxies to limit outgoing traffic to only necessary ports for that particular network segment.
            user info: "https://attack.mitre.org/mitigations/M1030/"
            ->  router.computer.os.commonlyUsedPortsConnection,
                router.computer.os.uncommonlyUsedPortsConnection,
                router.computer.os.service.externalRemoteServices,
                router.computer.os.windows.componentObjectModelAndDistributedCOM,
                router.computer.os.windows.remoteDesktopProtocol,
                router.computer.os.windows.windowsRemoteManagement
    }
}

category Hardware {
    asset Computer {
        | infectedComputer
            ->  router.internalNetwork.applicationLayerConnexion,
                os.commonlyUsedPortsConnection,
                os.uncommonlyUsedPortsConnection,
                os.fileDeletion,
                os.windows.attemptPowerShell,
                os.windows.accountManipulation,
                os.service.attemptExternalRemoteServices,
                peripheralDevice.removableMedia.infectedMedia,
                peripheralDevice.removableMedia.dataFromRemovableMedia

        | infectedWindowsComputer
                
        | infectedLinuxComputer

        | infectedMacOSComputer 

        | unresponsive

        | exfiltrationOverPhysicalMedium
            ->  peripheralDevice.removableMedia.dataExfiltration

        E microphoneExists
            <-  peripheralDevice.microphone
            ->  peripheralDevice.microphone.collectAudio

        & collectAudio
            ->  os.dataCollected
        
        | peripheralDeviceDiscovery
            user info: "Adversaries may attempt to gather information about attached peripheral devices and components connected to a computer system. The information may be used to enhance their awareness of the system and network environment or may be used for further actions."

        E webcamExists
            <-  peripheralDevice.webcam
            ->  peripheralDevice.webcam._collectVideo

        E videoCallApplicationExists
            <-  os.service.videoCallApplication
            ->  os.service.videoCallApplication._collectVideo

        | collectVideo
            ->  os.dataCollected
    }

    asset HardwareAddition extends Computer {
        | physicalAccess [Bernoulli(0.11)*Exponential(8)]
            developer info: "Sources for distribution result: https://www.if-insurance.com/large-enterprises/insight/risk-consulting-magazine/risk-consulting-2019-1/physical-security-controls-of-it-and-ics, https://go.crowdstrike.com/rs/281-OBQ-266/images/Report2020CrowdStrikeGlobalThreatReport.pdf"
            ->  hardwareAdditions
            
        & hardwareAdditions 
            user info: "Adversaries may introduce computer accessories, computers, or networking hardware into a system or network that can be used as a vector to gain access." 
            ->  os.executeCode  
    }

    asset PeripheralDevice {

    }

    asset Webcam extends PeripheralDevice {
        | _collectVideo @hidden
            ->  computer.collectVideo
    }

    asset RemovableMedia extends PeripheralDevice {
        | dataFromRemovableMedia
            ->  computer.os.sensitiveDataCollected

        | infectedMedia

        | dataExfiltration
    }
    
    asset Microphone extends PeripheralDevice {
        | collectAudio
            ->  computer.collectAudio
    }
}

associations {
    User [user] * <--Uses--> * [computer] Computer
    User [user] * <--Logsin--> * [userAccount] UserAccount
    User [user] * <--Logsin--> * [adminAccount] AdminAccount
    AdminAccount [adminAccount] * <--Manages--> * [userAccount] UserAccount
    UserAccount [userAccount] * <--Access--> * [os] OS
    AdminAccount [adminAccount] * <--Access--> * [os] OS
    WindowsAdmin [windowsAdmin] * <--Extends--> * [adminAccount] AdminAccount
    Root [rootAdmin] * <--Extends--> * [adminAccount] AdminAccount
    OS [os] * <--Extends--> * [windows] Windows
    OS [os] * <--Extends--> * [linux] Linux  
    OS [os] * <--Extends--> * [macOS] MacOS
    SysAdmin [sysAdmin] * <--Operates--> * [windows] Windows
    OS [os] * <--Runs--> * [service] Service
    Computer [computer] * <--Operates--> * [os] OS
    Router [router] * <--Communicates--> * [computer] Computer
    Firewall [firewall] * <--Protect--> * [router] Router
    InternalNetwork [internalNetwork] * <--Connects--> * [router] Router
    ExternalNetwork [externalNetwork] * <--Connects--> * [router] Router
    ThirdpartySoftware [thirdpartySoftware] * <--Extends--> * [service] Service
    VideoCallApplication [videoCallApplication] * <--Extends--> * [service] Service
    Browser [browser] * <--Access--> * [service] Service
    InternalNetwork [internalNetwork] * <--Extends--> * [networkSharedDrive] NetworkSharedDrive
    Computer [computer] * <--Serves--> * [peripheralDevice] PeripheralDevice
    HardwareAddition [hardwareAddition] * <--Extends--> * [computer] Computer
    Microphone [microphone] * <--Extends--> * [peripheralDevice] PeripheralDevice
    Webcam [webcam] * <--Extends--> * [peripheralDevice] PeripheralDevice
    RemovableMedia [removableMedia] * <--Extends--> * [peripheralDevice] PeripheralDevice
}
